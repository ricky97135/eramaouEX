@CHAR_CUSTOM, ARG , ARG:1
#DIM L_PAGE
#DIM L_LCOUNT
#DIM PRICE

SWAP TARGET, ARG
L_LCOUNT = LINECOUNT
;初始價格
CALL CHARA_COST , TARGET
PRICE = RESULT

$DRAW_PAGE
REDRAW 0
CLEARLINE LINECOUNT - L_LCOUNT
L_LCOUNT = LINECOUNT

CUSTOMDRAWLINE =
IF ARG:1 == 0
    PRINTFORML 設定角色屬性（%SAVESTR:TARGET%）	角色現價值為{PRICE}		<{L_PAGE+1}/5>
ELSE	
    PRINTFORML 修改角色屬性（%SAVESTR:TARGET%）			<{L_PAGE+1}/5>
ENDIF	
DRAWLINE

IF INRANGE(L_PAGE, 0, 2)
	CALL CHAR_CUSTOM_TALENT_PAGE(L_PAGE,ARG:1)
ELSE
	CALL CHAR_CUSTOM_LOOK_PAGE(L_PAGE-3)
ENDIF

IF LINECOUNT - L_LCOUNT < 27
	REPEAT 27 + L_LCOUNT - LINECOUNT
		PRINTL
	REND
ENDIF

DRAWLINE
PRINTLC [997] 前一頁
PRINTLC [999] 確定

SIF ARG:1 == 0
    PRINTLC [996] 取消
	
PRINTLC [998] 后一頁
PRINTL

$INPUT_LOOP
INPUT

IF RESULT == 999
    ;素質檢查
	IF ARG:1 == 0
		CALL TALENT_EMPTY_CHECK ,TARGET
		SIF RESULT == 1
			GOTO DRAW_PAGE
		;魔族新勇者隨機成為黑暗救世主、九尾、混沌龍
        SIF TALENT:A:314 == 9
            TALENT:A:322 = RAND:3 + 191		
		;人物初始設定（抄自CHARA_MAKE）
		A = TARGET
		;經驗級別
		CFLAG:A:9 = 1
		EXP:A:80 = 0
		;初始位置
		CFLAG:A:1 = 0
		;職業、基礎
		CALL CM_BASE
		;善惡
		CALL CM_KIND
		;服裝設定
		CALL CM_CLOTH	;SET_CHAR_CLOTH
		;一人稱の設定
		CALL RANDOM_SELF_CALL, A
		;年齢or身長などを表示する設定の場合は身體データを設定
		IF GETBIT(FLAG:5,12) || GETBIT(FLAG:5,15)
			CALL CHAR_BODY_GENERATE_WAPPED, A
		ENDIF
		;妊娠設定出產日期
		IF TALENT:A:153 || TALENT:A:341 || TALENT:A:342
		CFLAG:A:110 = DAY + 10 + RAND:6
		CFLAG:A:111 = 0
		ENDIF
		;再次檢查價格
		CALL CHARA_COST , TARGET
		PRICE = RESULT
		PRINTFORML %SAVESTR:TARGET%的最終價格是{PRICE}點，可以嗎？
		PRINTL [1] 好，就是這樣了！  [2] 我還想再修改一下。 
		INPUT
		$LOOP
		IF RESULT == 1
		   IF MONEY < PRICE 
			  PRINTW 錢不夠，還是重新設定吧！
			  GOTO DRAW_PAGE
		   ELSE
			  PRINTFORMW 花費金錢{PRICE}點，%SAVESTR:TARGET%現已加入豪華午餐。
			  MONEY -= PRICE  
			  EX_FLAG:4444 -= PRICE
			  REDRAW 1
			  SWAP TARGET, ARG
			  RETURN 
		   ENDIF
		ELSEIF  RESULT == 2
		   GOTO DRAW_PAGE 
		ELSE   
		   GOTO LOOP
		ENDIF
    ELSEIF ARG:1 == 1
	    PRINTFORMW %SAVESTR:TARGET%現已加入豪華午餐。
        REDRAW 1
	    SWAP TARGET, ARG
		RETURN 
    ENDIF		
ELSEIF RESULT == 998
	SIF L_PAGE < 4
		L_PAGE ++
	GOTO DRAW_PAGE
ELSEIF RESULT == 997
	SIF L_PAGE > 0
		L_PAGE --
	GOTO DRAW_PAGE
ELSEIF RESULT == 996
	REDRAW 1
	SWAP TARGET, ARG
	DELCHARA ARG
	RETURN 
ENDIF

IF INRANGE(L_PAGE, 0, 2)	
	CALL CHAR_CUSTOM_TALENT_DEAL(RESULT)
	;儲存CHAR_CUSTOM_TALENT_DEAL輸出
    P = RESULT	
	;每次素質變更檢查價格
	CALL CHARA_COST , TARGET
	PRICE = RESULT
	;恢復CHAR_CUSTOM_TALENT_DEAL輸出
	RESULT = P
	
ELSE
	CALL CHAR_CUSTOM_LOOK_DEAL(RESULT)
	SIF TALENT:308 <= 100
	    TALENT:115 = 0
ENDIF

IF RESULT == 0
	GOTO DRAW_PAGE
ELSE
	CLEARLINE 1
	REUSELASTLINE 無效值
	GOTO INPUT_LOOP
ENDIF

RESTART

@CHAR_CUSTOM_TALENT_DEAL(L_TAL)
#DIM L_TAL
#DIM L_I

IF !INRANGE(L_TAL, 0,500)
	RETURN -1
ENDIF

TALENT:TARGET:L_TAL = ! TALENT:TARGET:L_TAL
CALL CONFLICT_CHECK(L_TAL)

;胸圍變化
L_I = TALENT:L_TAL
IF GROUPMATCH(L_TAL,109,110,114,116,119)
	TALENT:109 = 0
	TALENT:110 = 0
	TALENT:114 = 0
	TALENT:119 = 0
	TALENT:116 = 0
	TALENT:L_TAL = L_I
	TRYCALL CHAR_BUST_REGENERATE_WAPPED, TARGET
ENDIF
;口上唯一
IF GROUPMATCH(L_TAL,160,161,162,163,164,166,172,173,174)
	TALENT:160 = 0
	TALENT:161 = 0
	TALENT:162 = 0
	TALENT:163 = 0
	TALENT:164 = 0
	TALENT:166 = 0
	TALENT:172 = 0
	TALENT:173 = 0
	TALENT:174 = 0
	TALENT:L_TAL = 1
ENDIF
SIF TALENT:174 == 1
	TALENT:122 = 1
SIF TALENT:166 == 1
	TALENT:122 = 0
;職業唯一
IF INRANGE(L_TAL,200,220)
    FOR L_I ,200 , 221
	    TALENT:L_I = 0
	NEXT
    TALENT:L_TAL = 1
ENDIF	
;精英固定魔族
SIF L_TAL == 220
    TALENT:314 = 9
SIF !(TALENT:314 == 9)
	TALENT:220 = 0
;龍族有角
SIF TALENT:314 == 5
	TALENT:264 = 1
;扶她及男人才有童貞
SIF !TALENT:121 && !TALENT:122
    TALENT:1 = 0
;纖細體型不肥胖
SIF TALENT:308 <= 100
	TALENT:115 = 0
RETURN 0

@CONFLICT_CHECK(ARG)
#DIM L_A
#DIM L_B
{
#DIM CONST PAIRS = 
	0,75, 30,75, 
	10,12,	11,13,	13,18, 14,16,	15,17,	17,18,	
	20,23,	21,23,	22,23,
	20,63,	21,63,	22,63,
	23,24,	25,26,	27,28,
	30,31,	32,33,	
	35,36,	40,41,	42,43,	
	44,45,	50,51,	61,62,	62,64,
	70,71,	74,150, 76,85,
	74,75, 74,77, 74,78, 75,77, 75,78, 77,78, 122,78,
	79,80, 79,81, 79,82, 79,122, 
	80,81, 80,82, 81,82,	
	99,100,		101,102,
	103,104,	105,106, 103,122, 104,122,
	107,108,	111,112,	
	109,110, 109,114, 109,116, 119,109, 119,116, 119,114, 119,110, 122,109, 122,110, 122,114, 122,116, 122,119,
	110,114, 110,116, 114,116,
	121,122,	153,154,	99,263, 153,122, 154,122, 130,122, 155,122, 157,122,
	155,156, 10,161, 26,161,
	60,150, 0,122,
	248,256, 244,253, 244,255, 253,255,
	259,260,
}
	LOCAL = 0
	$SEARCH_LOOP
	LOCAL = FINDELEMENT(PAIRS,ARG,LOCAL)
	;#;PRINTFORML {LOCAL}
	IF LOCAL >= 0	
		L_A = PAIRS:(LOCAL /2 *2)
		L_B = PAIRS:(LOCAL /2 *2 +1)
		;#;PRINTFORML {LOCAL} {L_A} {L_B} {ARG}
		IF TALENT:L_A && TALENT:L_B
			TALENT:L_A = 0
			TALENT:L_B = 0
			TALENT:ARG = 1
		ENDIF
		LOCAL ++
		GOTO SEARCH_LOOP
	ENDIF
; WAIT


@CHAR_CUSTOM_TALENT_PAGE(ARG=0,ARG:1)
#DIM L_I
CALL PRINT_SINGLE_TALENT()

IF ARG == 0


PRINTL ■=== 基本素質 ===■
FOR L_I, 0, 10
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 性格 ===■
FOR L_I, 10, 20
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 性態度 ===■
FOR L_I, 20, 30
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 少女心 ===■
FOR L_I, 30, 40
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 體質 ===■
FOR L_I, 40, 50
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 技術 ===■
FOR L_I, 50, 60
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 潔癖度 ===■
FOR L_I, 60, 65
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 正直度 ===■
FOR L_I, 69, 74
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 特殊性癖 ===■
FOR L_I, 74, 79
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


ELSEIF ARG == 1


PRINTL ■=== 性癖 ===■
FOR L_I, 79, 90
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 魅力 ===■
FOR L_I, 91, 99
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 身體特徵 ===■
FOR L_I, 99, 117
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT(119)
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 混雜 ===■
CALL PRINT_SINGLE_TALENT(117)
CALL PRINT_SINGLE_TALENT(118)
CALL PRINT_SINGLE_TALENT(121)
FOR L_I, 122, 160
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 性格（口上） ===■
FOR L_I, 160, 165
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT(166)
CALL PRINT_SINGLE_TALENT(172)
CALL PRINT_SINGLE_TALENT(173)
CALL PRINT_SINGLE_TALENT(174)
IF ARG:1 == 1
    FOR L_I, 165, 180
	SIF L_I == 166 || L_I == 172 || L_I == 173 || L_I == 174
	    CONTINUE
	CALL PRINT_SINGLE_TALENT(L_I)
    NEXT
ENDIF	
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 賣春相關 ===■
FOR L_I, 180, 190
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


ELSEIF ARG == 2


PRINTL ■=== 體調不良 ===■
FOR L_I, 190, 200
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 職業 ===■

FOR L_I, 200, 230
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 強化素質 ===■
FOR L_I, 230, 240
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 戰鬥技能 ===■
FOR L_I, 240, 270
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 特殊素質 ===■
FOR L_I, 270, 289
	SIF L_I == 280 || L_I == 281 || L_I == 283
		CONTINUE
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 境遇 ===■
FOR L_I, 290, 300
	SIF L_I == 292
		CONTINUE
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


PRINTL ■=== 精英 ===■
FOR L_I, 471, 490
	CALL PRINT_SINGLE_TALENT(L_I)
NEXT
CALL PRINT_SINGLE_TALENT()


ENDIF

	
@PRINT_SINGLE_TALENT(ARG = -1)
IF ARG < 0
	SIF !LINEISEMPTY()
		PRINTL
	RESETCOLOR
	LOCAL = 0
	RETURN 0
ENDIF

SIF STRLENS(TALENTNAME:ARG) < 1
	RETURN 

IF TALENT:ARG
	RESETCOLOR
ELSE
	SETCOLORBYNAME GRAY
ENDIF

SIF LOCAL % 6 == 0
	PRINT   

PRINTBUTTON @"[%TALENTNAME:ARG%]", ARG
LOCAL ++

SIF LOCAL % 6 == 0
	PRINTL

RETURN LOCAL

@TALENT_EMPTY_CHECK ,ARG
;檢查素質完備程度，防止自我介紹、口上為空時的錯誤
#DIM CHECK ,100
CHECK:1 = 0
CHECK:2 = 0
FOR CHECK ,0 ,300
    IF TALENT:ARG:CHECK == 1
	   SELECTCASE CHECK
	         ;口上
	         CASE 160 TO 174
			     CHECK:1 = 1
			 ;職業	 
			 CASE 200 TO 220
                 CHECK:2 = 1			 
	   ENDSELECT
    ENDIF
NEXT	

SIF CHECK:1 == 0
   PRINTL 需要設定性格（口上）
SIF CHECK:2 == 0
   PRINTL 需要有【近衛】及【後代】之外的職業設定
SIF TALENT:ARG:300 == 0
   PRINTL 需要設定髮色
SIF TALENT:ARG:301 == 0
   PRINTL 需要設定頭髮狀態
SIF TALENT:ARG:303 == 0
   PRINTL 需要設定頭髮修剪方式
SIF TALENT:ARG:304 == 0
   PRINTL 需要設定髮型
SIF TALENT:ARG:305 == 0
   PRINTL 需要設定眼型
SIF TALENT:ARG:306 == 0
   PRINTL 需要設定瞳色
SIF TALENT:ARG:307 == 0
   PRINTL 需要設定唇型
SIF TALENT:ARG:309 == 0
   PRINTL 需要設定乳頭
SIF TALENT:ARG:310 == 0
   PRINTL 需要設定陰毛狀態
SIF TALENT:ARG:312 == 0
   PRINTL 需要設定魅力點
SIF TALENT:ARG:313 == 0
   PRINTL 需要設定癖好
SIF TALENT:ARG:317 == 0
   PRINTL 需要設定喜歡的東西
SIF TALENT:ARG:220 && TALENT:ARG:319 == 0
   PRINTL 精英需要設定精英種族
   
IF CHECK:1 && CHECK:2 &&  TALENT:ARG:300 && TALENT:ARG:301 && TALENT:ARG:303 && TALENT:ARG:304 && TALENT:ARG:305 && TALENT:ARG:306 && TALENT:ARG:307 && TALENT:ARG:309 && TALENT:ARG:310 && TALENT:ARG:312 && TALENT:ARG:313 && TALENT:ARG:317 && !(TALENT:ARG:220 && TALENT:ARG:319 == 0)
   CALL CHARA_FIRST_XP , ARG
   PRINTW 人物設定完成
   RETURN 0
ELSE
   PRINTW 請返回繼續設定
   RETURN 1
ENDIF   


@CHARA_COST , ARG
;計算人物價格，基礎價格50萬，各素質價格按以下設定
#DIM COST 
#DIM L_I
COST = 0
FOR L_I ,0 ,500
    IF TALENT:ARG:L_I == 1
	   SELECTCASE L_I

	         CASE 10,13,14,17,37,41,99,125,131,132,134,140,141,142,143
                 COST += 150000	 

			 CASE 11,12,15,16,84,100,133
                 COST -= 50000

			 CASE 20,21,22,24,27,32,34,43,51,79,82
			     COST -= 100000

             CASE 23,25,28,33,42,50,57
			     COST += 150000				 

			 CASE 30,46,56,62,109,112,115,116,135,256
			     COST -= 100000

			 CASE 31,52,55,60,61,63,64,80,81,88,89,110,111,114,118,136,155,157,180,181,182,183,184,185,186,187,275 TO 279,471 TO 485
			     COST += 150000	 

			 CASE 0,1,70,72,91,92,93,102,104,106,108,113,117,124,126,153,154,254,271
			     COST += 150000
			 CASE 200 TO 220,221,222,
			     COST += 10000	 
			 CASE 69,71,101,103,105,107,273,280
			     COST -= 100000	 
			 CASE 9,122,123,150,151,152
                 COST -= 100000
			 CASE 73,74,75,76,77,78,130,230,231,232,233,272
			     COST += 150000
			 CASE 119,240 TO 252,257 TO 263,
			     COST += 200000
			 CASE 85,86
			     COST += 600000	 
			 ENDSELECT
    ENDIF
NEXT	
;粉毛加十萬
SIF TALENT:ARG:300 == 11
    COST += 100000
;素質加算價格不為負數
SIF COST < 0
    COST = 0
COST += 500000
RETURN COST	

@CHARA_FIRST_XP , ARG
$LOOP2
LOCAL = 0
LOCALS =
LOCAL:1 = 0
LOCALS:1 =
CFLAG:ARG:16 = LOCAL
CSTR:4 = %LOCALS%
CFLAG:ARG:15 = LOCAL:1
CSTR:3 = %LOCALS:1%
SIF EX_TALENT:ARG:2
    RETURN 0


PRINTL 設定初體驗
PRINTL 初吻對象是？
PRINTL [0] 不明 [1] 魔王 [993] 狂王 [994] 怪物 [995] 野狗 [999] 觸手 [996] 隨機 [997] 自定義輸入 [998] 無 
INPUT
LOCAL = RESULT
SELECTCASE LOCAL
      CASE 1
	       PRINTL 初吻位置是？
           PRINT [1] 唇 
		   SIF TALENT:MASTER:121 || TALENT:MASTER:122
		   PRINT [201] 陰莖
		   SIF !TALENT:MASTER:122
		   PRINT [301] 私處
		   PRINT [401] 肛門
		   INPUT
		   IF GROUPMATCH(RESULT,1,201,301,401)
		       LOCALS = %SAVESTR:MASTER%
		       LOCAL = RESULT
		   ELSE
           PRINTL 輸入錯誤，請重新開始。
		   GOTO LOOP2
		   ENDIF
	  CASE 995   
           PRINTL 初吻位置是？
		   PRINTL [1] 肛門 [2] 陰莖 [3] 嘴
		   INPUT
		   IF GROUPMATCH(RESULT,1,2,3)
		       LOCAL += RESULT
		   ELSE
           PRINTL 輸入錯誤，請重新開始。
		   GOTO LOOP2
		   ENDIF
	  CASE 996
	       LOCAL = -1
	  CASE 997
           $LOOP3	  
	       PRINTL 輸入初吻對像（留空將會隨機產生）：
		   INPUTS
		   LOCALS '= RESULTS
		   SELECTCASE STRLENS(LOCALS)
		   CASE IS > 16
			   PRINTFORMW 太長，請使用全形八字以下。
			   GOTO LOOP3
		   CASE IS > 0
			   PRINTFORMW 新建人物初吻對像為%LOCALS%。
		   CASEELSE
		       PRINTFORMW 隨機產生。
			   LOCAL = -1
		   ENDSELECT
		   IF !(LOCAL == -1)
			   PRINTL 初吻位置是？
			   PRINTL [1] 唇 [201] 陰莖 [301] 私處 [401] 肛門
			   INPUT
			   IF GROUPMATCH(RESULT,1,201,301,401)
				   LOCALS = %SAVESTR:MASTER%
				   LOCAL = RESULT
			   ELSE
			   PRINTL 輸入錯誤，請重新開始。
			   GOTO LOOP3
			   ENDIF
		   ENDIF
	  CASE 0
	  CASE 993     	   
	  CASE 994
      CASE 999
	  CASE 998
	      LOCAL = -2 
      CASEELSE
           PRINTL 輸入錯誤，請重新開始。
		   GOTO LOOP2	  
ENDSELECT		   
IF !TALENT:ARG:0
PRINTL 初體驗對象是？
PRINTL [1] 魔王 [101] 蠕蟲 [102] 觸手生物 [103] 野狗 [104] 怪物 [105] 狂王 [996] 隨機 [997] 自定義輸入 [998] 無 
INPUT
LOCAL:1 = RESULT
SELECTCASE LOCAL:1
      CASE 1
	  CASE 101   
      CASE 102
	  CASE 996
           LOCAL:1 = -1	  
	  CASE 997
           $LOOP4	  
	       PRINTL 輸入初體驗對像（留空將會隨機產生）：
		   INPUTS
		   LOCALS:1 '= RESULTS
		   SELECTCASE STRLENS(LOCALS:1)
		   CASE IS > 16
			   PRINTFORMW 太長，請使用全形八字以下。
			   GOTO LOOP4
		   CASE IS > 0
			   PRINTFORMW 新建人物初體驗對像為為%LOCALS:1%。
		   CASEELSE
		       PRINTFORMW 隨機產生。
			   LOCAL:1 = -1
		   ENDSELECT
	  CASE 998
	  CASE 103
	  CASE 104     	   
	  CASE 105
      CASEELSE
           PRINTL 輸入錯誤，請重新開始。
		   GOTO LOOP2	  
ENDSELECT		   
ENDIF
A = ARG		   
SIF !EX_TALENT:A:2
	CALL CM_NS_EXP
IF !(LOCAL == -1)
CFLAG:ARG:16 = LOCAL
CSTR:4 = %LOCALS%
ENDIF
IF !(LOCAL:1 == -1)
CFLAG:ARG:15 = LOCAL:1
CSTR:3 = %LOCALS:1%
ENDIF

PRINT 　
IF CFLAG:16 > -1
	LOCAL = CFLAG:16 - 1
	IF LOCAL == -1
		PRINT [初吻對像：不明]
	ELSEIF CFLAG:16 == 992
		PRINTFORM [初吻對像：%CSTR:4%]	
	ELSEIF CFLAG:16 == 993
		PRINT [初吻對像：狂王]
	ELSEIF CFLAG:16 == 994
		PRINT [初吻對像：怪物]
	ELSEIF CFLAG:16 == 995
		PRINT [初吻對像：怪物的陰莖]
	ELSEIF CFLAG:16 == 996
		PRINT [初吻對像：野狗的肛門]
	ELSEIF CFLAG:16 == 997
		PRINT [初吻對像：野狗的陰莖]
	ELSEIF CFLAG:16 == 998
		PRINT [初吻對像：野狗的嘴]
	ELSEIF CFLAG:16 == 999
		PRINT [初吻對像：觸手]
	ELSE
		PRINTFORM [初吻對像：%CSTR:4%的
		IF CFLAG:16 < 100
			PRINT 唇]
		ELSEIF CFLAG:16 < 300
			PRINT 陰莖]
		ELSEIF CFLAG:16 < 400
			PRINT 私處]
		ELSEIF CFLAG:16 < 500
			PRINT 肛門]
		ENDIF
	ENDIF
ENDIF

IF CFLAG:15 > 0
	LOCAL = CFLAG:15 - 1
	;初體験がバイブの場合
	IF CFLAG:15 == 101
		PRINT [初體驗對像：蠕蟲]
	;初體験が觸手生物の場合
	ELSEIF CFLAG:15 == 102
		PRINT [初體驗對像：觸手生物]
	;初體験が犬の場合
	ELSEIF CFLAG:15 == 103
		PRINT [初體驗對像：野狗]
	;初體験が怪物の場合
	ELSEIF CFLAG:15 == 104
		PRINT [初體驗對像：怪物]
	ELSEIF CFLAG:15 == 105
		PRINT [初體驗對像：狂王]
	ELSEIF LOCAL == 0
		PRINTFORM [初體驗對像：%SAVESTR:LOCAL%]
	ELSE
		PRINTFORM [初體驗對像：%CSTR:3%]
	ENDIF
ENDIF
PRINTL 這樣就可以了嗎？
PRINTL [0] 好的 [1] 還是改一下吧
INPUT
SIF RESULT == 1
    GOTO LOOP2
RETURN 0