;アイテム消耗品全般

;----------------------------------------
@USE_EX_ITEM,ARGS
#DIM APPRAISE
;----------------------------------------
;條件を満たすとアイテムをひとつ使用する
;ARGS = タイミング

LOCAL = 560

REPEAT 5
	LOCAL:1 = CFLAG:A:LOCAL
	APPRAISE = 0
	
	;未鑑定品チェック
	IF LOCAL:1 > 1000
		LOCAL:1 -= 1000
		APPRAISE = 1
	ENDIF
	
	;RESULTは使用チェック
	RESULT = 0
	
	;HPが60%以下だと草藥使用
	IF BASE:A:0 < (MAXBASE:A:0 * 6) / 10 && LOCAL:1 == 400
		CALL HARB_ITEM, APPRAISE
	;気力が60%以下だと回覆藥水使用
	ELSEIF BASE:A:1 < (MAXBASE:A:1 * 6) / 10 && LOCAL:1 == 401
		CALL POTION_ITEM, APPRAISE
	;HPが80%以下だと回覆之杖使用
	ELSEIF BASE:A:0 < (MAXBASE:A:0 * 8) / 10 && LOCAL:1 == 402
		CALL HEAL_ROD_ITEM, APPRAISE
	;気力が80%以下だと精神之杖使用
	ELSEIF BASE:A:1 < (MAXBASE:A:1 * 8) / 10 && LOCAL:1 == 403
		CALL MIND_ROD_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 404
		CALL POWER_SEED_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 405
		CALL DEF_SEED_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 406
		CALL EXP_MEDAL_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 407
		CALL HP_SEED_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 408
		CALL MP_SEED_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 409
		CALL EXP_SILVER_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 410
		CALL DETOX_WORM_ITEM, APPRAISE
	;寶石箱はランダム使用。USE HELIX FOSSIL
	ELSEIF LOCAL:1 == 411 && RAND:5 == 0
		CALL JUEL_BOX_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 412 && ARGS == "戰鬥中" && GETBIT(CFLAG:A:503,7) == 0
		;戦闘中かつ透明化狀態でない場合
		CALL INVISIBLE_POTION_ITEM, APPRAISE
	ELSEIF LOCAL:1 == 413 && ARGS == "戰鬥中" && GETBIT(CFLAG:A:503,8) == 0
		;戦闘中かつ英雄狀態でない場合
		CALL HERO_POTION_ITEM, APPRAISE
	ENDIF
	
	SIF RESULT != 0
		CFLAG:A:LOCAL = 0
	
	LOCAL += 1
REND




RETURN 0

;----------------------------------------
@SELL_EX_ITEM,ARG
#DIM SELL_FLAG
#DIM SELL_ALL
#DIM GET_MONEY
;----------------------------------------
;ランダムでアイテムをひとつ売る

LOCAL = 560
SELL_ALL = 0
GET_MONEY = 0

REPEAT 5
	LOCAL:1 = CFLAG:ARG:LOCAL
	SELL_FLAG = 0
	
	;未鑑定品は確定売卻
	IF LOCAL:1 > 1000
		LOCAL:1 -= 1000
		SELL_FLAG = 1
	ENDIF
	
	;ランダムで売卻
	SIF RAND:10 == 0
		SELL_FLAG = 1
	
	IF SELL_FLAG == 1
		CFLAG:ARG:LOCAL = 0
		SELL_ALL += 1
		GET_MONEY += 200
		IF LOCAL:1 == 411
			;寶石箱は売卻額が高い（合計1000）
			GET_MONEY += 800
		ENDIF
		
		;元商人の売卻ボーナス
		SIF TALENT:ARG:成為勇者前的生活 == 15
			GET_MONEY += 100
		
	ENDIF
	
	
	LOCAL += 1
REND

IF SELL_ALL > 0 && GET_MONEY > 0
	PRINTFORML %SAVESTR:ARG%賣掉了{SELL_ALL}個道具，獲得了{GET_MONEY}點資金。
	CFLAG:ARG:580 += GET_MONEY
ENDIF


RETURN 0

;---------------------------------------
@ADD_EX_ITEM, ARG:0, ARG:1, ARG:2
#DIM ITEM_MAX
;---------------------------------------
;アイテムを入手する。Xは入手するアイテムID。Aはキャラ
;ARG:0 == -1のときはランダム
;ARG:0 == -2のときは武器限定
;ARG:0 == -3のときは消耗品限定

;ARG:1 == 入手キャラ
;ARG:2 == 0=未鑑定品あり 1=購入 2=下賜

;アイテムを増やした時はここを増やす
ITEM_MAX = 14
SIF ARG:0 == -1
	ARG:0 = RAND:ITEM_MAX + 400

;ランダムで武器を入手する
IF RAND:4 == 0 || ARG:0 == -2
	;強度から見て更新の必要があるか判定
	LOCAL:0 = CFLAG:(ARG:1):501
	LOCAL:1 = CFLAG:(ARG:1):550 % 100000 / 1000
	w:0 = CFLAG:(ARG:1):550
	;顯示現有武器情況
	PRINTFORM 現持武器是
	CALL PRINT_EQUIPTYPE_WEAPON
	PRINTFORMDL ，等級{local:1}
	;現有武器等級高於層數不更換
	IF LOCAL:0 <= LOCAL:1
		PRINTFORMDL 沒有必要更換。
		RETURN 0
	ENDIF

	;無法裝備不更換	

	;新しい武器を追加するときはこのランダム値を弄ること
	W:8 = RAND:11 + 340
	
	;觸手裝備は除外
	SIF W:8 == 349
		W:8 = 340
	
	CALL GET_EQUIP_NUM

;*****************************獲得新裝備，判斷是否屬於可以使用的裝備**************************
	W:1 = W:0 % 1000
	IF USEABLE_EQUIPMENT(ARG:1,W:1) == 0
		CALL PRINT_EQUIPTYPE_WEAPON
		PRINTFORML 不是其趁手的裝備
		RETURN 0
	ENDIF
;**********************************************************************************************

	;入手階層に応じた強度になる
	W:0 += CFLAG:(ARG:1):501 * 1000
	
	;接頭語追加
	;接頭語を拡張するときはこの値を弄ること
	W:0 += RAND:10 * 100000
	
	CFLAG:(ARG:1):550 = W:0
	;顯示更換后武器

	PRINTFORM 經過考慮，更換成
	CALL PRINT_EQUIPTYPE_WEAPON
	PRINTFORML 了
	RETURN W:8
ENDIF

SIF ARG:0 == -3
	ARG:0 = RAND:ITEM_MAX + 400

;侵攻中の勇者の場合、未鑑定品を入手する可能性がある
SIF CFLAG:(ARG:1):1 == 2 && RAND:4 == 0
	ARG:0 += 1000

;購入の場合、未鑑定品にならない
SIF ARG:0 > 1000 && ARG:2 >= 1
	ARG:0 -= 1000

;即座に鑑定を行う。神官・盜賊は鑑定成功率が高い
LOCAL:2 = 0
IF ARG:0 > 1000
	IF TALENT:(ARG:1):202 || TALENT:(ARG:1):203
		LOCAL:2 = RAND:2
	ELSE
		LOCAL:2 = RAND:5
	ENDIF
ENDIF

IF ARG:0 > 1000 && LOCAL:2 == 0
	PRINT 【鑑定成功】
	ARG:0 -= 1000
ENDIF

LOCAL = 560

REPEAT 5
	LOCAL:1 = CFLAG:(ARG:1):LOCAL
	
	IF LOCAL:1 <= 0
		CFLAG:(ARG:1):LOCAL = ARG:0
		PRINTFORM %SAVESTR:(ARG:1)%將
		
		CALL EX_ITEM_NAME,ARG:0
		
		IF ARG:2 == 1
			PRINTW 買下了
		ELSE
			PRINTW 入手了
		ENDIF
		
		RETURN ARG:0
	ENDIF
	
	LOCAL += 1
REND

RETURN 0

;*************************判斷裝備是否可以使用**********************
@USEABLE_EQUIPMENT,ARG,ARG:1 
#FUNCTION
;--------------------------------------
;戒指
SIF ARG:1 >= 0 && ARG:1 <= 20
	RETURNF 1

;40劍
;41法杖
;42鞭子
;43匕首
;44手裡劍
;45弓箭
;46權杖
;47戰錘
;48鐮刀
;50細劍
;51偃月刀
;52指虎

IF TALENT:ARG:200
	IF ARG:1 == 40 || ARG:1 == 42 || ARG:1 == 43 || ARG:1 == 47 || ARG:1 == 48 || ARG:1 == 50 || ARG:1 == 51 || ARG:1 == 52
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

IF TALENT:ARG:201
	IF ARG:1 == 41 || ARG:1 == 46
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

IF TALENT:ARG:202
	IF ARG:1 == 41 || ARG:1 == 46
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

IF TALENT:ARG:203
	IF ARG:1 == 42 || ARG:1 == 43 || ARG:1 == 44 || ARG:1 == 50 || ARG:1 == 52
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

IF TALENT:ARG:205
	IF ARG:1 == 40 || ARG:1 == 42 || ARG:1 == 43 || ARG:1 == 47 || ARG:1 == 48 || ARG:1 == 50
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

IF TALENT:ARG:206
	IF ARG:1 == 40 || ARG:1 == 41 || ARG:1 == 42 || ARG:1 == 43 || ARG:1 == 44 || ARG:1 == 45 || ARG:1 == 46 || ARG:1 == 47 || ARG:1 == 48 || ARG:1 == 50 || ARG:1 == 51
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

IF TALENT:ARG:207
	IF ARG:1 == 43 || ARG:1 == 44 || ARG:1 == 50 || ARG:1 == 52
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF

IF TALENT:ARG:208
	IF ARG:1 == 43 || ARG:1 == 45
		RETURNF 1
	ELSE
		RETURNF 0
	ENDIF
ENDIF
;--------------------------------------
@HARB_ITEM, ARG:0
;--------------------------------------
;HP500回復。ID400

LOCAL = 0

;未鑑定品は1/2の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:2 == 0
	LOCAL = 1

IF LOCAL == 1
	;欲情點數
	JUEL:A:5 += 30
ELSE
	BASE:A:0 += 500
	SIF BASE:A:0 > MAXBASE:A:0
		BASE:A:0 = MAXBASE:A:0
ENDIF

	
IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 草藥
	ELSE
		PRINT 【謎之草】
	ENDIF
	
	PRINT 吃掉了！
	
	IF LOCAL == 1
		PRINTFORML 這是快樂草的葉片！(欲情點數+30)
	ELSE
		PRINTFORML (HP+500)
	ENDIF
	
ENDIF

RETURN 1

;--------------------------------------
@POTION_ITEM, ARG:0
;--------------------------------------
;気力500回復。ID401

LOCAL = 0

;未鑑定品は1/2の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:2 == 0
	LOCAL = 1

IF LOCAL == 1
	;精液經驗
	EXP:A:20 += 1
ELSE
	BASE:A:1 += 500
	SIF BASE:A:1 > MAXBASE:A:1
		BASE:A:1 = MAXBASE:A:1
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 恢復藥水
	ELSE
		PRINT 【謎之液體】
	ENDIF
	
	PRINT 喝掉了！
	
	IF LOCAL == 1
		PRINTFORML 嘔，這是精液！(精液經驗+1)
	ELSE
		PRINTFORML (氣力+500)
	ENDIF
	
ENDIF

RETURN 1

;--------------------------------------
@HEAL_ROD_ITEM, ARG:0
;--------------------------------------
;HP200回復。たまに壊れる。ID402

LOCAL = 0

;未鑑定品は1/2の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:2 == 0
	LOCAL = 1

IF LOCAL == 1
	;自慰經驗
	EXP:A:10 += 1
	;陰核點數
	JUEL:A:0 += 10
	;欲情點數
	JUEL:A:5 += 20
ELSE
	BASE:A:0 += 200
	SIF BASE:A:0 > MAXBASE:A:0
		BASE:A:0 = MAXBASE:A:0
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 回覆之杖
	ELSE
		PRINT 【謎之杖】
	ENDIF
	
	PRINT 揮動著！
	
	IF LOCAL == 1
		PRINTFORML 這是被詛咒的振動杖！
		PRINTFORML %SAVESTR:A%在催眠狀態下開始自慰
		PRINTL 自慰經驗+1
		PRINTL 欲情點數+20
		PRINTL 陰核點數+10
	ELSE
		PRINTFORML (HP+200)
	ENDIF
	
ENDIF

;未鑑定品は確定で破壊
SIF ARG:0 > 0
	RETURN 1

;杖は破壊されないことがある
SIF RAND:3 > 0
	RETURN 0

RETURN 1

;--------------------------------------
@MIND_ROD_ITEM, ARG:0
;--------------------------------------
;気力200回復。たまに壊れる。ID403

LOCAL = 0

;未鑑定品は1/2の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:2 == 0
	LOCAL = 1

;A経験が無い場合無しに
SIF EXP:A:1 == 0
	LOCAL = 0

IF LOCAL == 1
	;自慰經驗
	EXP:A:10 += 1
	;肛門經驗
	EXP:A:1 += 1
	;肛門點數
	JUEL:A:2 += 10
	;欲情點數
	JUEL:A:5 += 20
ELSE
	BASE:A:1 += 200
	SIF BASE:A:1 > MAXBASE:A:1
		BASE:A:1 = MAXBASE:A:1
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 精神之杖
	ELSE
		PRINT 【謎之杖】
	ENDIF
	
	PRINT 揮動著！
	
	IF LOCAL == 1
		PRINTFORML 這是被詛咒的肛門振動杖！
		PRINTFORML %SAVESTR:A%在催眠狀態下開始自慰
		PRINTL 自慰經驗+1
		PRINTL 肛門經驗+1
		PRINTL 欲情點數+20
		PRINTL 肛門點數+10
	ELSE
		PRINTFORML (氣力+200)
	ENDIF
	
ENDIF

;未鑑定品は確定で破壊
SIF ARG:0 > 0
	RETURN 1

;杖は破壊されないことがある
SIF RAND:3 > 0
	RETURN 0

RETURN 1

;-----------------------------------------
@POWER_SEED_ITEM, ARG:0
;-----------------------------------------
;力之種子。ID404

LOCAL = 0

;未鑑定品は1/4の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:4 == 0
	LOCAL = 1

IF LOCAL == 1
	;陰核感度上昇
	IF TALENT:A:101 == 1
		;C鈍感消去
		TALENT:A:101 = 0
	ELSEIF TALENT:A:101 == 0 && TALENT:A:102 == 0
		;C敏感
		TALENT:A:102 = 1
	ELSE
		;陰核點數
		JUEL:A:0 += 10
	ENDIF
ELSE
	CFLAG:A:13 += 1
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 力量種子
	ELSE
		PRINT 【謎之種子】
	ENDIF
	
	PRINT 吃掉了！
	
	IF LOCAL == 1 &&  TALENT:A:122
		PRINTFORML 吃掉的是陰莖敏感之種子！(陰莖感度上昇)
	ELSEIF LOCAL == 1 &&  TALENT:A:121
		PRINTFORML 吃掉的是陰莖敏感之種子！(陰莖感度上昇)
	ELSEIF LOCAL == 1
		PRINTFORML 吃掉的是陰蒂敏感之種子！(陰蒂感度上昇)
	ELSE
		PRINTFORML （攻擊力+1）
	ENDIF
	
ENDIF

RETURN 1

;-----------------------------------------
@DEF_SEED_ITEM, ARG:0
;-----------------------------------------
;守之種子。ID405

LOCAL = 0

;未鑑定品は1/4の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:4 == 0
	LOCAL = 1

IF LOCAL == 1
	;乳房感度上昇
	IF TALENT:A:107 == 1
		;B鈍感消去
		TALENT:A:107 = 0
	ELSEIF TALENT:A:107 == 0 && TALENT:A:108 == 0
		;B敏感
		TALENT:A:108 = 1
	ELSE
		;乳房點數
		JUEL:A:14 += 10
	ENDIF
ELSE
	CFLAG:A:14 += 1
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 守護種子
	ELSE
		PRINT 【謎之種子】
	ENDIF
	
	PRINT 吃掉了！
	
	IF LOCAL == 1
		PRINTFORML 吃掉的是乳頭勃起之種子！(乳房感度上昇)
	ELSE
		PRINTFORML (防禦力+1)
	ENDIF
	
ENDIF

RETURN 1

;-----------------------------------------
@EXP_MEDAL_ITEM, ARG:0
;-----------------------------------------
;經驗硬幣。ID406

LOCAL = 0

;未鑑定品は1/2の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:2 == 0
	LOCAL = 1

IF LOCAL == 1
	;恭順點數
	JUEL:A:4 += 10
ELSE
	EXP:A:80 += 50
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%將
	
	IF ARG:0 == 0
		PRINT 經驗硬幣
	ELSE
		PRINT 【謎之硬幣】
	ENDIF
	
	PRINT 使用了！
	
	IF LOCAL == 1
		PRINTFORML 這個是魔鬼硬幣！(恭順點數+10)
	ELSE
		PRINTFORML （經驗值+50）
	ENDIF
	
ENDIF

RETURN 1

;-----------------------------------------
@HP_SEED_ITEM, ARG:0
;-----------------------------------------
;命之種子。ID407

LOCAL = 0

;未鑑定品は1/4の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:4 == 0
	LOCAL = 1

;オトコはV感度上昇しない
SIF TALENT:A:122
	LOCAL = 0

IF LOCAL == 1
	;私處感度上昇
	IF TALENT:A:103 == 1
		;V鈍感消去
		TALENT:A:103 = 0
	ELSEIF TALENT:A:103 == 0 && TALENT:A:104 == 0
		;V敏感
		TALENT:A:104 = 1
	ELSE
		;欲情點數
		JUEL:A:5 += 10
	ENDIF
ELSE
	MAXBASE:A:0 += 10
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 生命種子
	ELSE
		PRINT 【謎之種子】
	ENDIF
	
	PRINT 吃掉了！
	
	IF LOCAL == 1
		PRINTFORML 吃掉的是私處成長之種子！(私處感度上昇)
	ELSE
		PRINTFORML （最大HP+10）
	ENDIF
	
ENDIF


RETURN 1

;-----------------------------------------
@MP_SEED_ITEM, ARG:0
;-----------------------------------------
;心之種子。ID408

LOCAL = 0

;未鑑定品は1/4の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:4 == 0
	LOCAL = 1

IF LOCAL == 1
	;肛門感度上昇
	IF TALENT:A:105 == 1
		;A鈍感消去
		TALENT:A:105 = 0
	ELSEIF TALENT:A:105 == 0 && TALENT:A:106 == 0
		;A敏感
		TALENT:A:106 = 1
	ELSE
		;肛門點數
		JUEL:A:2 += 10
	ENDIF
ELSE
	MAXBASE:A:1 += 5
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%把
	
	IF ARG:0 == 0
		PRINT 心之種子
	ELSE
		PRINT 【謎之種子】
	ENDIF
	
	PRINT 吃掉了！
	
	IF LOCAL == 1
		PRINTFORML 吃掉的是肛門柔化之種子！！(肛門感度上昇)
	ELSE
		PRINTFORML （最大氣力+5）
	ENDIF
	
ENDIF

RETURN 1

;-----------------------------------------
@EXP_SILVER_ITEM, ARG:0
;-----------------------------------------
;經驗銀幣。ID409

LOCAL = 0

;未鑑定品は1/2の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:2 == 0
	LOCAL = 1

IF LOCAL == 1
	;屈服點數
	JUEL:A:6 += 30
ELSE
	EXP:A:80 += 150
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%將
	
	IF ARG:0 == 0
		PRINT 經驗銀幣
	ELSE
		PRINT 【謎之銀幣】
	ENDIF
	
	PRINT 使用了！
	
	IF LOCAL == 1
		PRINTFORML 這是黑暗銀幣！(屈服點數+10)
	ELSE
		PRINTFORML （經驗值+150）
	ENDIF
	
ENDIF


RETURN 1

;-----------------------------------------
@DETOX_WORM_ITEM,ARG
;-----------------------------------------
;聖水。ID410


LOCAL = 0

;未鑑定品は1/2の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:2 == 0
	LOCAL = 1



IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%將
	
	IF ARG:0 == 0
		PRINT 聖水
	ELSE
		PRINT 【謎之液體】
	ENDIF
	
	PRINT 喝掉了！
	
	IF LOCAL == 1
		PRINTFORML 這是淫魔的小便！(欲情點數+15)
	ENDIF
	
ENDIF


IF LOCAL == 1
	IF TALENT:A:成為勇者前的生活 == 12
		;元聖女はさらに
		SIF FLAG:5 & 32
			PRINTFORML 聖女的力量受到了污染……（屈服の珠+10）
		;屈服の珠
		JUEL:A:6 += 10
	ENDIF
	;欲情の珠
	JUEL:A:5 += 15
ELSE
	
	IF TALENT:A:成為勇者前的生活 == 12
		;元聖女は攻撃/防禦強化の特殊ボーナス
		SIF FLAG:5 & 32
			PRINTFORML 聖水增強了聖女的力量……（攻撃+1/防禦+1）
		CFLAG:A:13 += 1
		CFLAG:A:14 += 1
	ENDIF
	
	CALL ITEM_DETOX,A
	PRINTW (善惡值上升了:1)
	CALL KARMA, A, 1
ENDIF

RETURN 1

;-----------------------------------------
@JUEL_BOX_ITEM,ARG
;-----------------------------------------
;墮落の寶石箱。ID411
;未鑑定品も鑑定品も効果は同じ

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%は
	
	IF ARG:0 == 0
		PRINT 墮落的寶石箱
	ELSE
		PRINT 【迷之箱子】
	ENDIF
	
	PRINT を使用した！（屈服の珠+5）
	
ENDIF


;屈服の珠
JUEL:A:6 += 5

;迎撃中の場合、捨てる
SIF CFLAG:A:1 == 3
	RETURN 1

;壊れない
RETURN 0

;-----------------------------------------
@INVISIBLE_POTION_ITEM,ARG
;-----------------------------------------
;透明化の薬。ID412

LOCAL = 0

;未鑑定品は1/3の確率でえっちなアイテム
SIF ARG:0 == 1 && RAND:3 == 0
	LOCAL = 1

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%將
	
	IF ARG:0 == 0
		PRINT 透明化之藥
	ELSE
		PRINT 【謎之液體】
	ENDIF
	
	PRINT 喝掉了！
	
	IF LOCAL == 1
		PRINTFORML 竟然是衣服透明化之藥啊！
		PRINTFORML %SAVESTR:A%變得赤身裸體了……(恥情點數+15)
	ENDIF
	
ENDIF

IF LOCAL == 1
	IF TALENT:A:35
		;恥じらいはさらに
		SIF FLAG:5 & 32
			PRINTFORML 臉像西紅柿一樣紅透了……（屈服點數+10）
		;屈服の珠
		JUEL:A:6 += 10
	ENDIF
	;恥情の珠
	JUEL:A:8 += 15
ELSE
	SIF FLAG:5 & 32
		PRINTFORML *透明化使得迴避力上升了*
	;透明化ON（忍術相當）
	SETBIT CFLAG:A:503,7
	
ENDIF

;消耗品
RETURN 1

;-----------------------------------------
@HERO_POTION_ITEM, ARG:0
#DIM UP_VALUE
;-----------------------------------------
;英雄の薬。ID413

LOCAL = 0
UP_VALUE = CFLAG:A:9 / 10 + 10

;未鑑定品は1/3の確率で呪われたアイテム
SIF ARG:0 == 1 && RAND:3 == 0
	LOCAL = 1

IF LOCAL == 1
	;恐怖の珠
	JUEL:A:10 += 10
ELSE
	;攻撃防禦UP
	CFLAG:A:11 += UP_VALUE
	CFLAG:A:12 += UP_VALUE
	;英雄ON（迴避小UP）
	SETBIT CFLAG:A:503,8
ENDIF

IF FLAG:5 & 32
	PRINTFORM %SAVESTR:A%將
	
	IF ARG:0 == 0
		PRINT 英雄之藥
	ELSE
		PRINT 【謎之液體】
	ENDIF
	
	PRINT 喝掉了！
	
	IF LOCAL == 1
		PRINTFORML 竟然是恐懼藥水啊！(恐怖點數+10)
	ELSE
		PRINTFORML (攻擊・防禦+{UP_VALUE}、迴避UP)
	ENDIF
	
ENDIF

RETURN 1

;----------------------------------------
@EX_ITEM_NAME,ARG:0
;----------------------------------------
;アイテムの名前を表示する
;ARG:0 がアイテムナンバー

IF ARG:0 > 1000
	PRINT 【未鑑定品】
ELSE
	PRINTFORM %ITEMNAME:(ARG:0)%
ENDIF

RETURN 1

