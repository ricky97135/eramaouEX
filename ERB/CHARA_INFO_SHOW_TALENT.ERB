;==================================================
@SHOW_TALENT_CONDITION
; 顯示素質詳細
;ref to <GET_SPECIALTALENT.ERB>
;--------------------------------------------------
#DIM SEXSKILL_COUNT
#DIM SEXSKILL_EXP, 4
;--------------------
;助手/愛/淫亂
;--------------------
;【助手可能】
;調教経験2000/【愛】|【淫亂】
IF TALENT:76 || TALENT:85
	IF TALENT:9 
		CALL STC_COLOR_INVALID
	ELSEIF CFLAG:0 == 2
		CALL STC_COLOR_ACHIEVE
	ELSE
		CALL STC_COLOR_DEFAULT
	ENDIF
	PRINTFORM 助手條件： 
	IF CFLAG:2 >= 2000
		CALL STC_COLOR_TRUE
	ELSE
		CALL STC_COLOR_FALSE
	ENDIF
	CALL STC_PRINTC("[好感度 200%]",15)
	
	CALL STC_SAYNO_MARK(3)
	
	CALL STC_COLOR_TRUE
	SIF TALENT:76
		PRINTFORM [%TALENTNAME:76%]
	SIF TALENT:85
		PRINTFORM [%TALENTNAME:85%]
	PRINTL
ENDIF

;【愛】TALENT:85 調教経験1000
IF !TALENT:76 && !TALENT:184
	;調教経験1000/順從3/侍奉精神LV3/屈服刻印LV3/侍奉快樂經200
	;/NOT【淫亂】【求愛】
	CALL STC_LAB_TAL(85)
	
	IF CFLAG:2 >=1000
		CALL STC_COLOR_TRUE
	ELSE
		CALL STC_COLOR_FALSE
	ENDIF
	CALL STC_PRINTC("[好感度 100%]",15)
	
	CALL STC_SAY_ABL(10,3)
	CALL STC_SAY_ABL(16,3)
	CALL STC_SAY_MARK(2,3)
	CALL STC_SAY_EXP(21, EXPLV:5)
	;CALL STC_SAYNO_TAL(184)
	;CALL STC_SAYNO_TAL(76)
	CALL STC_SAYNO_MARK(3)
	PRINTL
ENDIF

;【淫亂】TALENT:76 調教経験1000
IF !TALENT:85
	;調教経験1000/慾望3/ABCV感覚合計10/快楽・屈服刻印LV3/異常經驗3/NOT【愛】
	CALL STC_LAB_TAL(76)
	
	IF CFLAG:2 >=1000
		CALL STC_COLOR_TRUE
	ELSE
		CALL STC_COLOR_FALSE
	ENDIF
	CALL STC_PRINTC("[好感度 100%]",15)
	
	CALL STC_SAY_ABL(11,3)
	;CALL STC_SAYSUM_ABL(10,0,1,2,3)
	CALL STC_SAY_ABCV(10)
	CALL STC_SAY_MARK(1,3)
	CALL STC_SAY_MARK(2,3)
	CALL STC_SAY_EXP(50,3)
	;CALL STC_SAYNO_TAL(85)
	CALL STC_SAYNO_MARK(3)
	PRINTL
ENDIF



;--------------------
;特殊技能素質
;--------------------
;【擅用舌頭】
CALL STC_LAB_TAL(52)
IF TALENT:51
	;【學習緩慢】
	;技巧Lv7/侍奉技術Lv7/口交經驗1500
	CALL STC_SAY_ABL(12,7)
	CALL STC_SAY_ABL(13,7)
	CALL STC_SAY_EXP(22,1500)
ELSE
;【學習緩慢】無
;技巧Lv5/侍奉技術Lv5/口交經驗1000
	CALL STC_SAY_ABL(12,5)
	CALL STC_SAY_ABL(13,5)
	CALL STC_SAY_EXP(22,1000)
ENDIF
PRINTL


;--------------------
;特殊性癖素質
;--------------------


;【施虐狂】
;抖S氣質Lv４\技巧Lv４\施虐快樂經驗３００
CALL STC_LAB_TAL(83)
CALL STC_SAY_ABL(20,4)
CALL STC_SAY_ABL(12,4)
CALL STC_SAY_EXP(33,300)
PRINTL

;【受虐狂】
;抖M氣質Lv４\露出癖Lv２\被虐快樂經驗３００
CALL STC_LAB_TAL(88)
CALL STC_SAY_ABL(21,4)
CALL STC_SAY_ABL(17,2)
CALL STC_SAY_EXP(30,300)
PRINTL

;【露出狂】
;露出癖Lv４/抖M氣質Lv２/調教自慰經驗＋放尿經驗＋噴奶經驗２００
CALL STC_LAB_TAL(89)
CALL STC_SAY_ABL(17,4)
CALL STC_SAY_ABL(21,2)
CALL STC_SAYSUM_EXP(200,11,31,54)
PRINTL

; 【牝犬】
;慾望Lv５/獸姦成癖Lv３/獸姦經驗３００
CALL STC_LAB_TAL(136)
CALL STC_SAY_ABL(11,5)
CALL STC_SAY_ABL(39,3)
CALL STC_SAY_EXP(56,300)
PRINTL

;--------------------
;特殊性感素質
;--------------------
;念のため
SEXSKILL_COUNT = 0
;カウント
SIF TALENT:74
	SEXSKILL_COUNT += 1
SIF TALENT:75
	SEXSKILL_COUNT += 1
SIF TALENT:77
	SEXSKILL_COUNT += 1
SIF TALENT:78
	SEXSKILL_COUNT += 1
SEXSKILL_EXP:1 = 100 + 50 * SEXSKILL_COUNT
SEXSKILL_EXP:2 = 100 + 10 * SEXSKILL_COUNT
SEXSKILL_EXP:3 = 300 + 50 * SEXSKILL_COUNT
;【自慰狂】
;陰蒂感覺４/調教自慰經驗１００/絕頂經驗１００
CALL STC_LAB_TAL(74)
IF SEXSKILL_COUNT == 0
	CALL STC_SAY_ABL(0,4)
	CALL STC_SAY_EXP(11,100)
	CALL STC_SAY_EXP(2,100)
ELSEIF TALENT:74 == 0 && SEXSKILL_COUNT > 0
	CALL STC_SAY_ABL(0,5)
	CALL STC_SAY_EXP(11,SEXSKILL_EXP:1)
	CALL STC_SAY_EXP(2,SEXSKILL_EXP:2)
ELSE
	RESETCOLOR
	;PRINT ■■■■■■■■■■■■■■■■■■■■■■
	PRINT_IMG "COVER_WHITE"
ENDIF
PRINTL

; 【弄乳狂】
;乳房感覺４/噴奶經驗１００/絕頂經驗１００
CALL STC_LAB_TAL(78)
IF SEXSKILL_COUNT == 0 && TALENT:122 == 0
	CALL STC_SAY_ABL(1,4)
	CALL STC_SAY_EXP(54,100)
	CALL STC_SAY_EXP(2,100)
ELSEIF TALENT:78 == 0 && SEXSKILL_COUNT > 0 && TALENT:122 == 0
	CALL STC_SAY_ABL(1,5)
	CALL STC_SAY_EXP(54,SEXSKILL_EXP:1)
	CALL STC_SAY_EXP(2,SEXSKILL_EXP:2)
ELSEIF TALENT:122 && SEXSKILL_COUNT == 0
	CALL STC_SAY_ABL(1,4)
	IF JUEL:14 >= SEXSKILL_EXP:1
		CALL STC_COLOR_TRUE
	ELSE
		CALL STC_COLOR_FALSE
	ENDIF
	CALL STC_PRINTC(@"[胸部點數{100,4}]")
	CALL STC_SAY_EXP(2,100)
ELSEIF TALENT:78 == 0 && SEXSKILL_COUNT > 0 && TALENT:122
	CALL STC_SAY_ABL(1,5)
	IF JUEL:14 >= SEXSKILL_EXP:1
		CALL STC_COLOR_TRUE
	ELSE
		CALL STC_COLOR_FALSE
	ENDIF
	CALL STC_PRINTC(@"[胸部點數{SEXSKILL_EXP:1,4}]")
	CALL STC_SAY_EXP(2,SEXSKILL_EXP:2)
ELSE
	RESETCOLOR
	;PRINT ■■■■■■■■■■■■■■■■■■■■■■
	PRINT_IMG "COVER_WHITE"
ENDIF
PRINTL

;【性愛狂】
;私處感覺４/私處經驗３００/絕頂經驗１００
CALL STC_LAB_TAL(75)
IF SEXSKILL_COUNT == 0 && TALENT:122 == 0
	CALL STC_SAY_ABL(2,4)
	CALL STC_SAY_EXP(0,300)
	CALL STC_SAY_EXP(2,100)
ELSEIF TALENT:75 == 0 && SEXSKILL_COUNT > 0 && TALENT:122 == 0
	CALL STC_SAY_ABL(2,5)
	CALL STC_SAY_EXP(0,SEXSKILL_EXP:3)
	CALL STC_SAY_EXP(2,SEXSKILL_EXP:2)
ELSEIF TALENT:122 && SEXSKILL_COUNT == 0
	CALL STC_SAY_ABL(0,4)
	CALL STC_SAY_EXP(5,300)
	CALL STC_SAY_EXP(2,100)
ELSEIF TALENT:122 && TALENT:75 == 0 && SEXSKILL_COUNT > 0
	CALL STC_SAY_ABL(0,4)
	CALL STC_SAY_EXP(5,SEXSKILL_EXP:3)
	CALL STC_SAY_EXP(2,SEXSKILL_EXP:1)
ELSE
	RESETCOLOR
	;PRINT ■■■■■■■■■■■■■■■■■■■■■■
	PRINT_IMG "COVER_WHITE"
ENDIF
PRINTL

;【尻穴狂】
;肛門感覺４/肛門快樂經驗３００/絕頂經驗１００
CALL STC_LAB_TAL(77)
IF SEXSKILL_COUNT == 0
	CALL STC_SAY_ABL(3,4)
	CALL STC_SAY_EXP(32,300)
	CALL STC_SAY_EXP(2,100)
ELSEIF TALENT:77 == 0 && SEXSKILL_COUNT > 0
	CALL STC_SAY_ABL(3,5)
	CALL STC_SAY_EXP(32,SEXSKILL_EXP:3)
	CALL STC_SAY_EXP(2,SEXSKILL_EXP:2)
ELSE
	RESETCOLOR
	;PRINT ■■■■■■■■■■■■■■■■■■■■■■
	PRINT_IMG "COVER_WHITE"
ENDIF
PRINTL



;--------------------
;強化素質
;--------------------

IF FLAG:73 <= 0

	;【淫核】
	;陰蒂感覺５/調教自慰經驗１００/絕頂經驗３００
	CALL STC_LAB_TAL(230)
	CALL STC_SAY_ABL(0,5)
	CALL STC_SAY_EXP(11,100)
	CALL STC_SAY_EXP(2,300)
	PRINTL

	;【淫乳】
	;乳房感覺５/噴奶經驗１００/絕頂經驗３００
	CALL STC_LAB_TAL(231)
	CALL STC_SAY_ABL(1,5)
	CALL STC_SAY_EXP(54,100)
	CALL STC_SAY_EXP(2,300)
	PRINTL

	;【淫壺】
	;私處感覺５/私處經驗３００/絕頂經驗３００
	CALL STC_LAB_TAL(232)
	CALL STC_SAY_ABL(2,5)
	CALL STC_SAY_EXP(0,300)
	CALL STC_SAY_EXP(2,300)
	PRINTL

	;【淫肛】
	;肛門感覺５/肛門快樂經驗３００/絕頂經驗３００
	CALL STC_LAB_TAL(233)
	CALL STC_SAY_ABL(3,5)
	CALL STC_SAY_EXP(32,300)
	CALL STC_SAY_EXP(2,300)
	PRINTL
	
	
	;【性豪】
	;強化素質を全部取得した場合
	CALL STC_LAB_TAL(272)
	CALL STC_SAY_TAL(230)
	CALL STC_SAY_TAL(231)
	CALL STC_SAY_TAL(232)
	CALL STC_SAY_TAL(233)
	PRINTL
ENDIF


;--------------------
;時常發情
;--------------------
IF FLAG:75 <= 0
	;潤滑の蓄積が700/欲情の蓄積が2250
	CALL STC_LAB_TAL(271)

	IF CFLAG:81 >= 700
		CALL STC_COLOR_TRUE
	ELSE
		CALL STC_COLOR_FALSE
	ENDIF
	CALL STC_PRINTC("[潤滑積蓄 700]",15)

	IF CFLAG:82 >= 2250
		CALL STC_COLOR_TRUE
	ELSE
		CALL STC_COLOR_FALSE
	ENDIF
	CALL STC_PRINTC("[欲情積蓄2250]")
	PRINTL
ENDIF

;--------------------
;精愛味覺
;--------------------
CALL STC_LAB_TAL(47)

IF TALENT:47
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF 

CALL STC_SEIIN_CHECK
PRINTFORML [飲精絕頂{RESULT,4}]


;--------------------
;妓女&傾城
;--------------------
IF !TALENT:180 
	;【妓女】
	CALL STC_LAB_TAL(180)
	IF TALENT:315 == 5
	;元妓女\賣淫經驗80\慾望Lv1\反抗刻印無
		CALL STC_SAY_ABL(11,1)
		CALL STC_SAY_EXP(74, 80)
		CALL STC_SAYNO_MARK(3)
	ELSE
	;賣淫經驗100\慾望Lv2\技巧Lv1\反抗刻印無
		CALL STC_SAY_ABL(12,1)
		CALL STC_SAY_ABL(11,2)
		CALL STC_SAY_EXP(74, 100)
		CALL STC_SAYNO_MARK(3)
	ENDIF
ELSE
	;【傾城】
	CALL STC_LAB_TAL(181)
	IF TALENT:315 == 5
	;元妓女\妓女修得\賣淫經驗160\反抗刻印無
		CALL STC_SAY_TAL(180)
		CALL STC_SAY_EXP(74, 160)
		CALL STC_SAYNO_MARK(3)
	ELSE
	;妓女修得\賣淫經驗200\反抗刻印無
		CALL STC_SAY_TAL(180)
		CALL STC_SAY_EXP(74, 200)
		CALL STC_SAYNO_MARK(3)
	ENDIF
ENDIF
PRINTL
;--------------------
;盲從
;--------------------
CALL STC_LAB_TAL(86)
IF TALENT:86
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF
RESETCOLOR
PRINT 【
CALL STC_SAY_TAL(85)
CALL STC_SAY_EXP(81,5)	
CALL STC_SAY_ABL(10,4)
RESETCOLOR
PRINT 】【
CALL STC_SAY_TAL(76)
CALL STC_SAY_EXP(81,10)	
CALL STC_SAY_ABL(10,5)
RESETCOLOR
PRINT 】
CALL STC_SAYNO_MARK(3)
PRINTL
RESETCOLOR


	
;==================================================	


@STC_PRINTC(ARGS, ARG = 15)
LOCAL = ARG - (STRLENS(ARGS) % ARG)
SIF LOCAL == ARG
	LOCAL = 0

PRINTS ARGS
PRINTS " " * LOCAL



; 繪製行頭 素質ARG
@STC_LAB_TAL(ARG)	
IF TALENT:9
	; TALENT:9 【崩壞】
	CALL STC_COLOR_INVALID
ELSEIF TALENT:ARG
	CALL STC_COLOR_ACHIEVE
ELSE
	CALL STC_COLOR_DEFAULT
ENDIF
LOCALS = %TALENTNAME:ARG%
SIF ARG == 230 && TALENT:122
	LOCALS = 絕倫

SIF STRLENS(LOCALS) <= 4
	LOCALS = %LOCALS,4,LEFT%條件
PRINTFORM %LOCALS,8,LEFT%： 



; 能力ARG >= ARG:1 
@STC_SAY_ABL(ARG, ARG:1)
IF ABL:ARG >= ARG:1
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF
IF ARG == 0 && TALENT:122
	CALL STC_PRINTC(@"[陰莖感覺 Lv{ARG:1}]")
ELSE
	CALL STC_PRINTC(@"[%ABLNAME:ARG,6,LEFT% Lv{ARG:1}]")
ENDIF

; 能力ARG >= ARG:1 
;@STC_SAY_JUEL(ARG, ARG:1)
;IF JUEL:ARG >= ARG:1
;	CALL STC_COLOR_TRUE
;ELSE
;	CALL STC_COLOR_FALSE
;ENDIF
;IF ARG == 0 && TALENT:122
;	CALL STC_PRINTC(@"[陰莖點數 {ARG:1,4}]")
;ELSE
;	CALL STC_PRINTC(@"[%JUELNAME:ARG,6,LEFT% {ARG:1,4}]")
;ENDIF

; 經驗ARG >= ARG:1 
@STC_SAY_EXP(ARG, ARG:1)
IF EXP:ARG >= ARG:1
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF
LOCALS = %EXPNAME:ARG%
SIF STRLENS(LOCALS) > 8
	LOCALS = %SUBSTRING(LOCALS,0,8)%
CALL STC_PRINTC(@"[%LOCALS,8,LEFT%{ARG:1,4}]")

; MARK:ARG >= ARG:1
@STC_SAY_MARK(ARG, ARG:1 = 0)
IF MARK:ARG >= ARG:1
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF
CALL STC_PRINTC(@"[%MARKNAME:ARG,6,LEFT% Lv{ARG:1}]")

; 素質ARG 有
@STC_SAY_TAL(ARG)
IF TALENT:ARG 
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF
LOCALS = %TALENTNAME:ARG%
SIF ARG == 230 && TALENT:122
	LOCALS = 絕倫
SIF STRLENS(LOCALS) <= 4
	LOCALS += "素質"
CALL STC_PRINTC(@"[%LOCALS%]",12)


; 印記ARG <= ARG:1
@STC_SAYNO_MARK(ARG, ARG:1 = 0)
IF MARK:ARG <= ARG:1
	CALL STC_COLOR_RIGHT
ELSE
	CALL STC_COLOR_ALERT
ENDIF
CALL STC_PRINTC(@"[%MARKNAME:ARG%]",12)
	
; 素質ARG <= ARG:1
@STC_SAYNO_TAL(ARG, ARG:1 = 0)
IF TALENT:ARG <= ARG:1
	CALL STC_COLOR_RIGHT
ELSE
	CALL STC_COLOR_ALERT
ENDIF
CALL STC_PRINTC(@"[%TALENTNAME:ARG%]",12)

	
; 經驗ARG:1 + 經驗ARG:2 + 經驗ARG:3 >= ARG:0
@STC_SAYSUM_EXP(ARG, ARG:1, ARG:2 = 0, ARG:3 = 0)
LOCAL = 0

REPEAT 3
	SIF ARG:(COUNT+1) > 0
		LOCAL += EXP:(ARG:(COUNT+1))
REND

IF LOCAL >= ARG
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF
LOCALS = [
LOCALS += SUBSTRING(EXPNAME:(ARG:1),0,8)
REPEAT 2
	SIF ARG:(COUNT+2) > 0
		LOCALS += "|" + SUBSTRING(EXPNAME:(ARG:(COUNT+2)),0,8)
REND
PRINTFORM %LOCALS%{ARG,4}]


; ABL:C + ABL:B + ABL:V + ABL:A >= ARG
@STC_SAY_ABCV(ARG)
LOCAL = 0
REPEAT 4	
	LOCAL += ABL:COUNT
REND
IF LOCAL >= ARG
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF
CALL STC_PRINTC(@"[四點感覺Lv{ARG}]")

; 能力ARG:1 + ARG:2 + ARG:3 + ARG:4 >= ARG
@STC_SAYSUM_ABL(ARG, ARG:1, ARG:2 = 0, ARG:3 = 0, ARG:4 = 0)
LOCAL = 0
REPEAT 4
	SIF ARG:(COUNT+1) > 0
		LOCAL += ABL:(COUNT+1)
REND

IF LOCAL >= ARG
	CALL STC_COLOR_TRUE
ELSE
	CALL STC_COLOR_FALSE
ENDIF

PRINTFORM [%ABLNAME:(ARG:1)%
REPEAT 3
	SIF ARG:(COUNT+2) > 0
		PRINTFORM |%ABLNAME:(ARG:(COUNT+2))%
REND
PRINTFORM  Lv{ARG}]




@STC_COLOR_TRUE
SETCOLORBYNAME White

@STC_COLOR_FALSE
SETCOLORBYNAME Gray

; deepSkyBlue LightGreen darkSeaGreen RoyalBlue

@STC_COLOR_ALERT
SETCOLORBYNAME LightSalmon

@STC_COLOR_RIGHT
SETCOLORBYNAME DarkSeaGreen

@STC_COLOR_INVALID 
SETCOLORBYNAME DarkRed

@STC_COLOR_ACHIEVE 
SETCOLOR 102,179,255

@STC_COLOR_DEFAULT
RESETCOLOR




;強制精飲絶頂の回數
@STC_SEIIN_CHECK
;ref <SEIIN.ERB>

;規定の回數
LOCAL = 50

;奴隷の素質により規定回數を増減する
;剛強
SIF TALENT:13 == 1
	LOCAL += 4
;保守的
SIF TALENT:24 == 1
	LOCAL += 4
;樂觀的
SIF TALENT:25 == 1
	LOCAL -= 2
;悲觀的
SIF TALENT:26 == 1
	LOCAL += 2
;戒備森嚴
SIF TALENT:27 == 1
	LOCAL += 5
;壓抑
SIF TALENT:32 == 1
	LOCAL += 4
;開放
SIF TALENT:33 == 1
	LOCAL -= 2
;不怕污臭
SIF TALENT:61 == 1
	LOCAL -= 2
;反感污臭
SIF TALENT:62 == 1
	LOCAL += 2
;接受快感
SIF TALENT:70 == 1
	LOCAL -= 2
;否定快感
SIF TALENT:71 == 1
	LOCAL += 4
;容易上癮
SIF TALENT:72 == 1
	LOCAL -= 5
;倒錯的
SIF TALENT:80 == 1
	LOCAL -= 2
;淫亂
SIF TALENT:76 == 1
	LOCAL -= 20

RETURN LOCAL

