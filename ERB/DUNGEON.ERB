;--------------------------------------------------
;@CALLME雄霸天
@DUNGEON, ARG:0
#DIM WALK
#DIM TURN
#DIM SIDEA
#DIM SIDEB
#DIM TURNEND
#DIM FLOOR
#DIM NO_BATTLE
#DIM ROOM
#DIMS MAPC
;--------------------------------------------------
;A・ARG:0が攻略中のキャラ（リーダー）
;D:20が侵攻度 D:1=1で帰還 D:4はトラップ試行回數
;D:0は上書きされないように欠番
;TURN      = 侵攻カウンタ
;SIDEA     = 仲間A
;SIDEB     = 仲間B
;TURNEND   = 誰かが敗北して冒険が中斷されるフラグ
;FLOOR     = 現在階層
;NO_BATTLE = 戦闘が発生しないフラグ
MAPC = 地下城
SIF CFLAG:(ARG:0):1 == 12
	MAPC = 迷宮
;行動完了の場合飛ばす
IF CFLAG:(ARG:0):530 == 1
	;迎撃中の場合、潛入行動
	SIF CFLAG:(ARG:0):1 == 3
		CALL DUNGEON_SPY, ARG:0
	RETURN 0
ENDIF

SIDEA = CFLAG:(ARG:0):531
SIDEB = CFLAG:(ARG:0):532
;進擊了要
TARGET = ARG:0
D:20 = CFLAG:(ARG:0):502
D:1 = 0
IF FLAG:5 & 32
	PRINTL  
	DRAWLINE
	IF CFLAG:(ARG:0):1 == 3 && CFLAG:(ARG:0):507 == 0
		PRINTFORML %SAVESTR:(ARG:0)%的隊伍，開始迎擊勇者了！！
	;迎撃時體力が回復していると迎撃再開
	ELSEIF CFLAG:(ARG:0):1 == 3 && CFLAG:(ARG:0):507 == 1
		IF (BASE:(ARG:0):0 * 100 / MAXBASE:(ARG:0):0 > 80) && (BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 > 80)
			PRINTFORML 體力恢復了的%SAVESTR:(ARG:0)%，再次開始迎擊勇者！！
			CFLAG:(ARG:0):507 = 0
		ELSE
			PRINTFORML %SAVESTR:(ARG:0)%仍然在恢復體力。
		ENDIF
	ELSEIF CFLAG:(ARG:0):507 == 1
		PRINTFORML %SAVESTR:(ARG:0)%的隊伍，想要逃離%MAPC%。
	ELSE
		PRINTFORML %SAVESTR:(ARG:0)%的隊伍開始攻略%MAPC%！！
	ENDIF
	DRAWLINE

	PRINTL  
	;コンフィグ「戦闘ログでのSKIP中斷」がONなら強制停止
	IF GETBIT(FLAG:5,9)
		FORCEWAIT
	ELSE
		WAIT
	ENDIF
ENDIF

;フラグオフ
CFLAG:(ARG:0):503 = 0
SIF SIDEA > 0
	CFLAG:SIDEA:503 = 0
SIF SIDEB > 0
	CFLAG:SIDEB:503 = 0

	
	
FOR TURN, 0, 5
	;バランス調整のため侵攻は一回で終了
	SIF TURN > 0
		BREAK
	
	;戦闘が発生しないフラグ初期化
	NO_BATTLE = 0
	
	IF FLAG:5 & 32
		PRINTFORM %SAVESTR:(ARG:0)%%MAPC%
	ENDIF
	
	WALK = RAND:20
	;速度UP
	FOR LOCAL,0,6
		WALK += RAND:10
	NEXT
	;引き返す
	SIF CFLAG:(ARG:0):507 != 0
		WALK *= -2
	
	;裝備効果(侵攻)
	W:8 = 17
	CALL EQUIP_CHECK
	SIF RESULT > 0
		WALK *= 2
	
	;裝備効果(試練)
	W:8 = 19
	CALL EQUIP_CHECK
	SIF RESULT > 0
		WALK /= 2
	
	;迷惑狀態
	IF CFLAG:(ARG:0):509 == 1
		IF RAND:3 == 0
			;たまに回復
			;ハメを防ぐため先に判定する
			CFLAG:(ARG:0):509 = 0
		ELSE
			WALK = 0
		ENDIF
		
		
	ENDIF
	
	FLOOR = CFLAG:(ARG:0):501
	IF FLAG:400
		;イベントダンジョン
		CALL CAMPAIGN_ROOM,FLOOR
		ROOM = RESULT
	ELSE
		ROOM = FLAG:(FLOOR + 349)
	ENDIF
	
	IF FLAG:5 & 32
		IF ROOM == 507 && TALENT:(ARG:0):180
			;娼館街かつ娼婦
			PRINTL 挑選著客人……
			WALK = 0
		ELSEIF WALK >= 1
			PRINTL 前進著。
		ELSEIF WALK <= -1
			PRINTL 急速撤退著。
		ELSE
			PRINTL 迷路了。
		ENDIF
		
		WAIT
		
		PRINTL ----------------------
		PRINTFORML    %MAPC%深處
		PRINTL ----------------------
		
		PRINTFORM 第{FLOOR}階層
	ENDIF
	IF CFLAG:(ARG:0):1 == 2 || CFLAG:(ARG:0):1 == 12
		D:20 += WALK
	ELSE
		X *= 2
		D:20 -= WALK
	ENDIF
	IF FLAG:5 & 32
		BARL D:20,100,50
	ENDIF
	
	;ダンジョン侵攻度チェック
	IF D:20 >= 100
		;階層滯在カウントをリセット
		CFLAG:(ARG:0):514 = 0
		IF CFLAG:(ARG:0):1 == 2 || CFLAG:(ARG:0):1 == 12
			IF FLAG:400 && CFLAG:(ARG:0):1 == 12
				;イベント中は踏破できるか判定がある
				CALL CAMPAIGN_QUEST,ARG:0
				IF RESULT
					;攻略成功
					PRINTFORML %SAVESTR:(ARG:0)%踏破這一層了！
				ELSE
					;攻略失敗。追い返される
					PRINTFORML %SAVESTR:(ARG:0)%放棄探索，開始回頭了。
					CFLAG:(ARG:0):507 = 1
					D:20 = 95
				ENDIF
			ELSE
				PRINTFORML %SAVESTR:(ARG:0)%踏破這一層了！
			ENDIF
			
			SIF FLAG:5 & 32
				PRINTFORML 搜刮戰利品中…
			
			CALL ADD_EX_ITEM, -1, ARG:0, 0
			
			SIF FLAG:5 & 32 && RESULT == 0
				PRINTFORML 沒找到什麼有用的。
			
			IF FLAG:400 > 0 && FLOOR >= 6
				PRINTFORML 到達了%MAPC%的盡頭………
				CALL CAMPAIGN_ENDING,ARG:0
				D:20 = 0
				BREAK
				
			ELSEIF FLOOR >= 9
				PRINTL 這裡是魔王的房間………
				IF TALENT:(ARG:0):122 == 0
					JUMP ENDING_2
				ELSEIF TALENT:(ARG:0):122
					PRINTFORML 作為冒險者而非勇者的他深知自己無法擊敗魔王。
					IF RAND:4 == 0
						PRINTFORML 但%SAVESTR:(ARG:0)%仍是向魔王發起了挑戰。
						IF (TALENT:MASTER:122 == 0 && ABL:MASTER:11 > 3) || (ABL:MASTER:11 > 3 && TALENT:MASTER:122 && ABL:MASTER:23 > 3) || ABL:MASTER:11 > 6
						;女魔王且慾望大於3、男魔王慾望3基3、慾求不滿的慾望6
							PRINTFORM %SAVESTR:0%察覺到了%SAVESTR:(ARG:0)%的氣息。
							CALL BEDROOM_BATTLE_MALE,ARG:0
						ELSE
							PRINTFORML 可想而知%SAVESTR:(ARG:0)%失敗了、成爲了%MAPC%里眾多奴隸的一員。
							CFLAG:(ARG:0):1 = 0
						ENDIF
					ELSE
						PRINTFORML %SAVESTR:(ARG:0)%放棄了成為英雄的念頭，開始回頭了。
						PRINTFORML 再次鼓起勇氣來到這裡可能會花些時間了。
						CFLAG:(ARG:0):507 = 1
						CFLAG:(ARG:0):508 = 7
						CFLAG:(ARG:0):521 = 7
						CFLAG:(ARG:0):520 = 8
					ENDIF
				ENDIF
				D:20 = 0
			ELSE
				;體力が有り余っている場合、もう1階層冒険してみる
				
				IF CFLAG:(ARG:0):520 < FLOOR
					LOCAL = 0
					IF BASE:(ARG:0):0 * 100 / MAXBASE:(ARG:0):0 < 90
						LOCAL++
					ELSEIF BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 < 90
						LOCAL++
					;仲間Aの體調も見る
					ELSEIF SIDEA > 0 && BASE:SIDEA:0 * 100 / MAXBASE:SIDEA:0 < 90
						LOCAL++
					ELSEIF SIDEA > 0 && BASE:SIDEA:1 * 100 / MAXBASE:SIDEA:1 < 90
						LOCAL++
					;仲間Bの體調も見る
					ELSEIF SIDEB > 0 && BASE:SIDEB:0 * 100 / MAXBASE:SIDEB:0 < 90
						LOCAL++
					ELSEIF SIDEB > 0 && BASE:SIDEB:1 * 100 / MAXBASE:SIDEB:1 < 90
						LOCAL++
					ENDIF
					IF LOCAL == 0
						PRINTFORML 狀態良好的%SAVESTR:(ARG:0)%的隊伍、向更深階層發起挑戰……
						CFLAG:(ARG:0):520 = FLOOR
					ENDIF
				ENDIF
				
				IF CFLAG:(ARG:0):520 < FLOOR
					PRINTFORML %SAVESTR:(ARG:0)%放棄探索，開始回頭了。
					CFLAG:(ARG:0):507 = 1
					;到達階層を記憶
					SIF CFLAG:(ARG:0):521 < FLOOR
						CFLAG:(ARG:0):521 = FLOOR
					D:20 = 95
				ELSE
					CFLAG:(ARG:0):501 += 1
					CFLAG:(ARG:0):508 += 1
					FLOOR += 1
					PRINTFORML %SAVESTR:(ARG:0)%向第{FLOOR}階層發起挑戰。
					D:20 = 0
				ENDIF
			ENDIF
		ELSE
			IF FLOOR >= 9
				PRINTFORML %SAVESTR:(ARG:0)%返回了魔王的房間。
				D:20 = 100
				CFLAG:(ARG:0):507 = 0
			ELSE
				CFLAG:(ARG:0):501 += 1
				FLOOR += 1
				PRINTFORML %SAVESTR:(ARG:0)%回到了第{FLOOR}階層。
				D:20 = 10
			ENDIF
		CFLAG:(ARG:0):503 += 1
		SIF SIDEA > 0
			CFLAG:SIDEA:503 += 1
		SIF SIDEB > 0
			CFLAG:SIDEB:503 += 1
		BREAK
		ENDIF
	ELSEIF D:20 <=0
		;階層滯在カウントをリセット
		CFLAG:(ARG:0):514 = 0
		IF CFLAG:(ARG:0):1 == 2 || CFLAG:(ARG:0):1 == 12
			SIF FLOOR == 5
				FLOOR = 1
			IF FLOOR <= 1
				PRINTFORML %SAVESTR:(ARG:0)%回到了%MAPC%外面。
				D:20 = 0
				
				;街でのイベント
				CALL DUNGEON_TOWN,ARG:0
				
				; ;アイテムの購入
				; SIF FLAG:5 & 32
					; PRINTFORML %SAVESTR:(ARG:0)%的隊伍到道具屋補給…
				
				; CALL ADD_EX_ITEM, -3, ARG:0, 1
				
				; IF SIDEA > 0
					; A = SIDEA
					; CALL ADD_EX_ITEM, -3, ARG:0, 1
				; ENDIF
				
				; IF SIDEB > 0
					; A = SIDEB
					; CALL ADD_EX_ITEM, -3, ARG:0, 1
				; ENDIF
				
				; A = ARG:0
				
				BREAK
			ELSE
				CFLAG:(ARG:0):501 -= 1
				FLOOR -= 1
				PRINTFORML %SAVESTR:(ARG:0)%回到了第{FLOOR}階層。
				D:20 = 90
			ENDIF
			CFLAG:(ARG:0):503 += 1
			SIF SIDEA > 0
				CFLAG:SIDEA:503 += 1
			SIF SIDEB > 0
				CFLAG:SIDEB:503 += 1
			
		ELSE
			PRINTFORML %SAVESTR:(ARG:0)%踏破了這一層！
			
			;拡張任務の失敗判定
			IF CFLAG:(ARG:0):500 == 3
				PRINTFORML %SAVESTR:(ARG:0)%企圖擴張設施，失敗了……
				PRINTFORMW %SAVESTR:(ARG:0)%使用回城魔法回來了。
				CFLAG:(ARG:0):1 = 6
				RETURN 0
			ENDIF
			
			IF FLOOR <= 1
				PRINTFORMW 再往前走就走出%MAPC%了………
				PRINTFORMW %SAVESTR:(ARG:0)%使用回城魔法回來了。
				IF CFLAG:(ARG:0):505 > 0
					CFLAG:(ARG:0):1 = 5
				ELSE
					CFLAG:(ARG:0):1 = 6
				ENDIF
				RETURN 0
			ELSE
				CFLAG:(ARG:0):501 -= 1
				FLOOR -= 1
				PRINTFORML %SAVESTR:(ARG:0)%向奪回第{FLOOR}階層發起挑戰。
				D:20 = 90
			ENDIF
		ENDIF
	ELSE
		;階層移動がない分岐
		;階層滯在カウントを+1
		CFLAG:(ARG:0):514 += 1
		
		;奴隷の場合カウントが溜まると帰還する
		IF CFLAG:(ARG:0):1 == 3 && CFLAG:(ARG:0):514 > 15
			PRINTFORML %SAVESTR:(ARG:0)%在此長期滯留感到十分疲乏……
			PRINTFORMW %SAVESTR:(ARG:0)%使用了歸還魔法回程了。
			IF CFLAG:(ARG:0):505 > 0
				CFLAG:(ARG:0):1 = 5
			ELSE
				CFLAG:(ARG:0):1 = 6
			ENDIF
			CFLAG:(ARG:0):514 = 0
			RETURN 0
		ENDIF
		
	ENDIF
	;SIF FLAG:5 & 32
	;	WAIT
	
	;設施効果
	;1/3の確率で受けるキャラが変わる
	IF RAND:3 == 0 && SIDEA > 0
		A = SIDEA
	ELSEIF RAND:2 == 0 && SIDEB > 0
		A = SIDEB
	ELSE
		A = ARG:0
	ENDIF
	CALL DUNGEON_ROOM,A
	;戦闘無なら1が加算される
	NO_BATTLE += RESULT
	
	;陷阱処理
	;1/3の確率で受けるキャラが変わる
	IF RAND:3 == 0 && SIDEA > 0
		A = SIDEA
	ELSEIF RAND:2 == 0 && SIDEB > 0
		A = SIDEB
	ELSE
		A = ARG:0
	ENDIF
	
	;裝備効果(陷阱誘発)
	W:8 = 20
	CALL EQUIP_CHECK
	IF RESULT > 0
		D:4 = RESULT
		CALL DUNGEON_TRAP
	ELSE
		D:4 = 0
		;裝備効果(陷阱避け)
		W:8 = 16
		CALL EQUIP_CHECK
		SIF RESULT < RAND:10
			CALL DUNGEON_TRAP
	ENDIF
	
	A = ARG:0
	;シュートでPTが分斷された時のためにここで一度SIDEA・SIDEBを再定義
	;ちょっと亂暴だけどここはSIFで分岐させる必要はない…ハズ
	SIDEA = CFLAG:(ARG:0):531
	SIDEB = CFLAG:(ARG:0):532
	
	;戦闘フェイズ
	
	IF CFLAG:(ARG:0):1 == 2
		IF FLAG:5 & 16
			SIF FLAG:5 & 32
				PRINTW 因為沒有敵人所以進行了訓練。（經驗值增加）
			EXP:(ARG:0):80 += CFLAG:MASTER:9
			SIF SIDEA > 0
				EXP:SIDEA:80 += CFLAG:MASTER:9
			SIF SIDEB > 0
				EXP:SIDEB:80 += CFLAG:MASTER:9
		ELSEIF NO_BATTLE > 0
			;戦闘未発生フラグ
			SIF FLAG:5 & 32
				PRINTW 因為沒有敵人所以進行了訓練。（經驗值増加）
			EXP:(ARG:0):80 += CFLAG:MASTER:9
			SIF SIDEA > 0
				EXP:SIDEA:80 += CFLAG:MASTER:9
			SIF SIDEB > 0
				EXP:SIDEB:80 += CFLAG:MASTER:9
		ELSE
			TURNEND = 0
			CALL DUNGEON_PARTY_BATTLE, ARG:0
			;陥落したか否か
			IF CFLAG:(ARG:0):1 != 2 && CFLAG:(ARG:0):1 != 3
				CALL GET_DOWN_ENEMY,ARG:0
				;善惡值が低いと、仲間を売って助かろうとする
				;そのうち口上も挾みたい……
				IF CFLAG:(ARG:0):151 <= -50 && (SIDEA > 0 || SIDEB > 0)
					PRINTFORML %SAVESTR:(ARG:0)%背叛了同伴，開始透露她們的位置。
					PRINTFORMW 帶著諂媚的神情出賣著同伴，拚死地乞求著饒命……
					SIF SIDEA > 0 && CFLAG:SIDEA:1 == 2
						CFLAG:SIDEA:1 = 0
					SIF SIDEB > 0 && CFLAG:SIDEB:1 == 2
						CFLAG:SIDEB:1 = 0
				ENDIF
				CALL PARTY_DEL, ARG:0
				TURNEND += 1
			ENDIF
			
			;仲間Aが陥落したかどうか
			IF SIDEA > 0 && CFLAG:SIDEA:1 != 2 && CFLAG:SIDEA:1 != 3
				CALL GET_DOWN_ENEMY,SIDEA
				CALL PARTY_DEL, SIDEA
				TURNEND += 1
			ENDIF
			
			;仲間Bが陥落したかどうか
			IF SIDEB > 0 && CFLAG:SIDEB:1 != 2 && CFLAG:SIDEB:1 != 3
				CALL GET_DOWN_ENEMY,SIDEB
				CALL PARTY_DEL, SIDEB
				TURNEND += 1
			ENDIF
			
			SIF TURNEND > 0
				BREAK
		ENDIF
		
		TURNEND = 0
		
		;善惡值によっては魔王に寢返る
		IF CFLAG:(ARG:0):151 <= -150 && CFLAG:(ARG:0):1 == 2
			CFLAG:(ARG:0):1 = 0
			CALL GET_DOWN_ENEMY,ARG:0
			IF SIDEA > 0 || SIDEB > 0
				PRINTFORML %SAVESTR:(ARG:0)%背叛了同伴，開始透露她們的位置。
				PRINTFORMW 似乎想把同伴當成投誠的禮物……
				SIF SIDEA > 0 && CFLAG:SIDEA:1 == 2
					CFLAG:SIDEA:1 = 0
				SIF SIDEB > 0 && CFLAG:SIDEB:1 == 2
					CFLAG:SIDEB:1 = 0
			ENDIF
			CALL PARTY_DEL, ARG:0
			
			;仲間Aが陥落したかどうか
			IF SIDEA > 0 && CFLAG:SIDEA:1 != 2 && CFLAG:SIDEA:1 != 3
				CALL GET_DOWN_ENEMY, SIDEA
				CFLAG:SIDEA:1 = 0
			ENDIF
			
			;仲間Bが陥落したかどうか
			IF SIDEB > 0 && CFLAG:SIDEB:1 != 2 && CFLAG:SIDEB:1 != 3
				CALL GET_DOWN_ENEMY, SIDEB
				CFLAG:SIDEB:1 = 0
			ENDIF
			TURNEND += 1
		ENDIF
		
		IF SIDEA > 0 && CFLAG:SIDEA:151 <= -150 && CFLAG:SIDEA:1 == 2
			CALL GET_DOWN_ENEMY, SIDEA
			CALL PARTY_DEL, SIDEA
			TURNEND += 1
		ENDIF
		
		IF SIDEB > 0 && CFLAG:SIDEB:151 <= -150 && CFLAG:SIDEB:1 == 2
			CALL GET_DOWN_ENEMY, SIDEB
			CALL PARTY_DEL, SIDEB
			TURNEND += 1
		ENDIF
		
		SIF TURNEND > 0
			BREAK
	ELSEIF CFLAG:(ARG:0):1 == 12
		;イベントダンジョン
		IF NO_BATTLE > 0
			;戦闘未発生フラグ
			SIF FLAG:5 & 32
				PRINTW 因沒有敵人而自己進行了訓練（經驗值增加）
			EXP:(ARG:0):80 += CFLAG:MASTER:9
			SIF SIDEA > 0
				EXP:SIDEA:80 += CFLAG:MASTER:9
			SIF SIDEB > 0
				EXP:SIDEB:80 += CFLAG:MASTER:9
		ELSE
			TURNEND = 0
			CALL DUNGEON_PARTY_BATTLE, ARG:0
			;陥落したか否か
			IF CFLAG:(ARG:0):1 != 2 && CFLAG:(ARG:0):1 != 3 && CFLAG:(ARG:0):1 != 12
				PRINTFORML %SAVESTR:(ARG:0)%被抓住了…
				CFLAG:(ARG:0):507 = 0
				CALL PARTY_DEL, ARG:0
				TURNEND += 1
			ENDIF
			
			;仲間Aが陥落したかどうか
			IF SIDEA > 0 && CFLAG:SIDEA:1 != 2 && CFLAG:SIDEA:1 != 3 && CFLAG:SIDEA:1 != 12
				PRINTFORML %SAVESTR:SIDEA%被抓住了…
				CFLAG:SIDEA:507 = 0
				CALL PARTY_DEL, SIDEA
				TURNEND += 1
			ENDIF
			
			;仲間Bが陥落したかどうか
			IF SIDEB > 0 && CFLAG:SIDEB:1 != 2 && CFLAG:SIDEB:1 != 3 && CFLAG:SIDEB:1 != 12
				PRINTFORML %SAVESTR:SIDEB%被抓住了…
				CFLAG:SIDEB:507 = 0
				CALL PARTY_DEL, SIDEB
				TURNEND += 1
			ENDIF
			
			SIF TURNEND > 0
				BREAK
		ENDIF
	;勇者と元勇者の戦闘
	ELSE
		CALL DUNGEON_BATTLE2_PARTY, ARG:0
		;陥落したか否か
		IF RESULT == 2
			CALL GET_DOWN_ENEMY, B
			CFLAG:(ARG:0):505 += 1
			CALL PARTY_DEL, B
		ELSEIF RESULT == 1
			;NTR以外なら勇者討伐數をチェックしてご褒美の有無
			CFLAG:(ARG:0):507 = 0
			IF CFLAG:(ARG:0):1 == 0
				IF CFLAG:(ARG:0):505 > 0
					CFLAG:(ARG:0):1 = 5
				ELSE
					CFLAG:(ARG:0):1 = 6
				ENDIF
			ENDIF
			;NTRれたなら勇者討伐數を０に
			SIF CFLAG:(ARG:0):1 == 9
				CFLAG:(ARG:0):505 = 0
			CALL PARTY_DEL, ARG:0
			TARGET = -1
			RETURN 0
		ENDIF
	ENDIF
	
	IF CFLAG:(ARG:0):1 == 3 && FLAG:5 & 16 && CFLAG:MASTER:9 > 0
		SIF FLAG:5 & 32
			PRINTFORMW %SAVESTR:ARG%和怪物們進行了訓練（經驗值增加）
		EXP:(ARG:0):80 += CFLAG:MASTER:9
		;訓練行動と合わせて1.5倍に増加
		SIF CFLAG:(ARG:0):500 == 5
			EXP:(ARG:0):80 += CFLAG:MASTER:9 / 2
	ELSEIF CFLAG:(ARG:0):1 == 3 && CFLAG:(ARG:0):500 == 5 && CFLAG:MASTER:9 > 0
		;迎撃時の訓練行動
		SIF FLAG:5 & 32
			PRINTFORMW %SAVESTR:ARG%和怪物們進行了訓練（經驗值增加）
		EXP:(ARG:0):80 += CFLAG:MASTER:9
	ENDIF
	
	;貞操帯のカギを探す
	IF CFLAG:(ARG:0):1 == 3 && CFLAG:(ARG:0):49 == 1 && CFLAG:(ARG:0):50 == 0
		PRINTL
		PRINTFORMW 探索%MAPC%的時候，在洞穴角落裡發現了發光的東西。%SAVESTR:(ARG:0)%在意地把它撿起來了。
		IF RAND:2 == 0
			PRINTFORMW ………是個舊獎章么，這種東西沒有價值啊。%SAVESTR:(ARG:0)%把它丟掉了。
		ELSEIF RAND:2 == 0
			PRINTFORMW ………是個被壓壞了的戒指么，想必是哪個被怪物襲擊的犧牲者的吧。%SAVESTR:(ARG:0)%把它丟掉了。
		ELSEIF RAND:2 == 0
			PRINTFORMW ………罐頭的蓋子么，這種東西沒有價值啊。%SAVESTR:(ARG:0)%把它丟掉了。
		ELSEIF RAND:2 == 0
			PRINTFORMW ………金屬片啊，想必是鎧甲或者盾牌的碎片吧。%SAVESTR:(ARG:0)%把它丟掉了。
		ELSEIF RAND:2 == 0
			PRINTFORMW ………注意到有細細的線連著誘導陷阱的機關！
			PRINTFORMW 但是機關卻沒有發動，似乎是時間久遠已經壞掉了。%SAVESTR:(ARG:0)%咒罵著把它丟掉了。
		ELSEIF RAND:2 == 0
			PRINTFORMW ………是髒了的鉆戒嗎。但是對%SAVESTR:(ARG:0)%來說，完全是多餘的東西。
		ELSE
			PRINTFORMW ………沒有看錯，是那時候被%NAME:MASTER%丟掉的貞操帶鑰匙。%SAVESTR:(ARG:0)%終於找到了貞操帶鑰匙！
			PRINTFORMW %SAVESTR:(ARG:0)%帶著陶醉的神情，鄭重地把它放到懷裡了………（%SAVESTR:(ARG:0)%拿著貞操帶鑰匙）
			CFLAG:(ARG:0):50 = 1
		ENDif
	ENDIF
	
	;冒険の疲れ
	BASE:(ARG:0):1 -= RAND:6
	SIF SIDEA > 0
		BASE:SIDEA:1 -= RAND:6
	SIF SIDEB > 0
		BASE:SIDEB:1 -= RAND:6
	
	;狀態判定
	CALL CHECK_STATUS,ARG:0

	;帰還するかどうか
	IF CFLAG:(ARG:0):507 == 1
		;すでに帰還中である
		PRINTFORML %SAVESTR:(ARG:0)%在%MAPC%內撤退（現在第{FLOOR}層）
	;帰還フラグを立てる判定
	;帰還フラグを立て、挫折した階層を記憶する
	ELSE
		IF SIDEA > 0 && SIDEB > 0
			IF result:3 >= 1
				PRINTFORML 有人瀕死，%SAVESTR:(ARG:0)%決定後退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 2
			ELSEIF result:2 >= 2
				PRINTFORML 大部分人重傷，%SAVESTR:(ARG:0)%決定後退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 2
			ELSEIF result:1 >= 3
				PRINTFORML 全員輕傷，%SAVESTR:(ARG:0)%決定後退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 1
			ELSEIF RESULT:5 >= 2 
				PRINTFORML 全員身體狀況不佳，%SAVESTR:(ARG:0)%決定後退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 1
			ELSE
				;攻略を続ける
				PRINTFORML %SAVESTR:(ARG:0)%決定繼續在%MAPC%內前進……（現在第{FLOOR}層）
			ENDIF
		ELSEIF SIDEA > 0 || SIDEB > 0
			IF result:3 >= 1
				PRINTFORML 有人瀕死，%SAVESTR:(ARG:0)%決定後退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 2
			ELSEIF result:2 >= 1 && result:1 >= 1
				PRINTFORML 各種傷勢，%SAVESTR:(ARG:0)%決定後退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 2
			ELSEIF RESULT:5 >= 2 
				PRINTFORML 全員身體狀況不佳，%SAVESTR:(ARG:0)%決定後退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 1
			ELSE
				;攻略を続ける
				PRINTFORML %SAVESTR:(ARG:0)%決定繼續在%MAPC%內前進……（現在第{FLOOR}層）
			ENDIF
		ELSE
			IF CFLAG:(ARG:0):534 >= 2 && TALENT:(ARG:0):10 == 1
				PRINTFORML 膽小的%SAVESTR:(ARG:0)%雖然只是受到輕傷，依然決定撤退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 1
			ELSEIF CFLAG:(ARG:0):534 >= 3
				PRINTFORML %SAVESTR:(ARG:0)%重傷了，決定撤退。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 1
			ELSEIF CFLAG:(ARG:0):534 == 4 && (TALENT:(ARG:0):12 == 1 || TALENT:(ARG:0):161 == 1 )
				PRINTFORML 堅毅的%SAVESTR:(ARG:0)%陷入瀕死，迫不得已決定撤退了。（現在第{FLOOR}層）
				CFLAG:(ARG:0):507 = 1
				CFLAG:(ARG:0):520 = FLOOR - 1
			ELSE
				PRINTFORML %SAVESTR:(ARG:0)%決定繼續在%MAPC%內前進……（現在第{FLOOR}層）
			ENDIF
		ENDIF
		
		;防止後退過度
		SIF !CFLAG:(ARG:0):520
			CFLAG:(ARG:0):520 = 1
	ENDIF
NEXT

;戦闘後探索
;1/3の確率で受けるキャラが変わる
IF RAND:3 == 0 && SIDEA > 0
	LOCAL = SIDEA
ELSEIF RAND:2 == 0 && SIDEB > 0
	LOCAL = SIDEB
ELSE
	LOCAL = ARG:0
ENDIF

CALL DUNGEON_BITCH(LOCAL)
CALL GET_JUNK_ITEM(LOCAL)

;寶箱を見つける
SIF CFLAG:(ARG:0):1 == 2 && RAND:4 == 0
	CALL EQUIP_SELECT
IF SIDEA > 0 && CFLAG:SIDEA:1 == 2 && RAND:4 == 0
	A = SIDEA
	CALL EQUIP_SELECT
ENDIF
IF SIDEB > 0 && CFLAG:SIDEB:1 == 2 && RAND:4 == 0
	A = SIDEB
	CALL EQUIP_SELECT
ENDIF

A = ARG:0

;アイテムの使用
CALL USE_EX_ITEM,"戰鬥后"
IF SIDEA > 0
	A = SIDEA
	CALL USE_EX_ITEM,"戰鬥后"
ENDIF
IF SIDEB > 0
	A = SIDEB
	CALL USE_EX_ITEM,"戰鬥后"
ENDIF

A = ARG:0

;移動を反映
CFLAG:(ARG:0):502 = D:20
SIF SIDEA > 0
	CFLAG:SIDEA:502 = D:20
SIF SIDEB > 0
	CFLAG:SIDEB:502 = D:20

;階層を反映
FLOOR = CFLAG:(ARG:0):501
SIF SIDEA > 0
	CFLAG:SIDEA:501 = FLOOR
SIF SIDEB > 0
	CFLAG:SIDEB:501 = FLOOR

;休憩フェイズ

;勇者に紛れ込んだ奴隷が暗躍します
IF SIDEA > 0 && CFLAG:SIDEA:1 == 3
	PRINTFORML %SAVESTR:SIDEA%在大家都熟睡后開始了奇妙的儀式……（同伴的善良值-1）
	CALL KARMA, ARG:0, -1
	SIF SIDEB > 0
		CALL KARMA, SIDEB, -1
ENDIF

IF SIDEB > 0 && CFLAG:SIDEB:1 == 3
	PRINTFORML %SAVESTR:SIDEB%在大家都熟睡后開始了奇妙的儀式……（同伴的善良值-1）
	CALL KARMA, ARG:0, -1
	SIF SIDEA > 0
		CALL KARMA, SIDEA, -1
ENDIF


;裝備効果(キャンプ)
W:8 = 18
CALL EQUIP_CHECK
IF RESULT > 0
	IF CFLAG:A:503 & 1
	ELSE
		CFLAG:A:503 += 1
	ENDIF
ENDIF

IF SIDEA > 0
	A = SIDEA
	W:8 = 18
	CALL EQUIP_CHECK
	IF RESULT > 0
		IF CFLAG:A:503 & 1
		ELSE
			CFLAG:A:503 += 1
		ENDIF
	ENDIF
ENDIF
IF SIDEB > 0
	A = SIDEB
	W:8 = 18
	CALL EQUIP_CHECK
	IF RESULT > 0
		IF CFLAG:A:503 & 1
		ELSE
			CFLAG:A:503 += 1
		ENDIF
	ENDIF
ENDIF

A = ARG:0


;裝備効果(キャンプ禁止)
W:8 = 19
CALL EQUIP_CHECK
SIF CFLAG:A:503 & 1 && RESULT > 0
	CFLAG:A:503 -= 1
IF SIDEA > 0
	A = SIDEA
	;裝備効果(キャンプ禁止)
	W:8 = 19
	CALL EQUIP_CHECK
	SIF CFLAG:A:503 & 1 && RESULT > 0
		CFLAG:A:503 -= 1
ENDIF
IF SIDEB > 0
	A = SIDEB
	;裝備効果(キャンプ禁止)
	W:8 = 19
	CALL EQUIP_CHECK
	SIF CFLAG:A:503 & 1 && RESULT > 0
		CFLAG:A:503 -= 1
ENDIF

A = ARG:0


IF CFLAG:(ARG:0):1 == 2 && CFLAG:(ARG:0):503 & 1 && FLOOR > 1
	IF FLAG:5 & 32
		PRINTL  
		DRAWLINE
		PRINTFORMW %SAVESTR:(ARG:0)%躲起來休息。
		DRAWLINE
		PRINTL 
	ENDIF
ENDIF
SIF FLAG:5 & 32
	PRINTL  
TARGET = -1
RETURN 0

;---------------------------------
@CHECK_STATUS, ARG:0, MODE = 0
#DIM SIDEA
#DIM SIDEB
#DIM STATUS,8
#DIM S1_HP
#DIM S1_MP
#DIM S2_HP
#DIM S2_MP
#DIM S3_HP
#DIM S3_MP
#DIM MODE
;---------------------------------
;ARG:0 = パーティーリーダー
;SIDEA = 仲間A
;SIDEB = 仲間B
;S1_HP = 輕傷HP百分比
;S1_MP = 輕傷MP百分比
;S2_HP = 重傷HP百分比
;S2_MP = 重傷MP百分比
;S3_HP = 瀕死HP百分比
;S3_MP = 瀕死MP百分比

IF CFLAG:ARG:533 == ARG
	SIDEA = CFLAG:ARG:531
	SIDEB = CFLAG:ARG:532
ELSE
	SIDEA = CFLAG:(CFLAG:ARG:533):531
	SIDEB = CFLAG:(CFLAG:ARG:533):532
	SIF SIDEA == ARG
		SIDEA = CFLAG:ARG:533
	SIF SIDEB == ARG
		SIDEB = CFLAG:ARG:533
ENDIF
S1_HP = 60
S1_MP = 50
S2_HP = 35
S2_MP = 30
S3_HP = 20
S3_MP = 10
varset STATUS

IF CFLAG:(ARG:0):1 == 2 || CFLAG:(ARG:0):1 == 3
	IF BASE:(ARG:0):0 * 100 / MAXBASE:(ARG:0):0 < S1_HP || BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 < S1_MP
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%輕傷。
		CFLAG:(ARG:0):534 = 2
		STATUS:1 += 1
	ELSEIF BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 < S1_MP
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%身體抱恙。
		CFLAG:(ARG:0):534 = 2
		STATUS:4 += 1
	ELSEIF BASE:(ARG:0):0 * 100 / MAXBASE:(ARG:0):0 < S2_HP || BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 < S2_MP
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%重傷。
		CFLAG:(ARG:0):534 = 3
		STATUS:2 += 1
	ELSEIF BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 < S2_MP
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%頭腦發昏。
		CFLAG:(ARG:0):534 = 3
		STATUS:5 += 1
	ELSEIF BASE:(ARG:0):0 * 100 / MAXBASE:(ARG:0):0 < S3_HP && BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 < S3_MP
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%瀕死。
		CFLAG:(ARG:0):534 = 4
		STATUS:3 += 1
	ELSEIF BASE:(ARG:0):0 * 100 / MAXBASE:(ARG:0):0 < S3_HP
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%瀕死。
		CFLAG:(ARG:0):534 = 4
		STATUS:3 += 1
	ELSEIF BASE:(ARG:0):1 * 100 / MAXBASE:(ARG:0):1 < S3_MP
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%氣絕。
		CFLAG:(ARG:0):534 = 4
		STATUS:6 += 1
	ELSE
		SIF !MODE
			PRINTFORML %SAVESTR:(ARG:0)%元氣滿滿。
		CFLAG:(ARG:0):534 = 1
		STATUS:0 += 1
	ENDIF
	IF  SIDEA > 0
		IF BASE:(SIDEA):0 * 100 / MAXBASE:(SIDEA):0 < S1_HP || BASE:(SIDEA):1 * 100 / MAXBASE:(SIDEA):1 < S1_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%輕傷。
			CFLAG:(SIDEA):534 = 2
			STATUS:1 += 1
		ELSEIF BASE:(SIDEA):1 * 100 / MAXBASE:(SIDEA):1 < S1_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%身體抱恙。
			CFLAG:(SIDEA):534 = 2
			STATUS:4 += 1
		ELSEIF BASE:(SIDEA):0 * 100 / MAXBASE:(SIDEA):0 < S2_HP || BASE:(SIDEA):1 * 100 / MAXBASE:(SIDEA):1 < S2_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%重傷。
			CFLAG:(SIDEA):534 = 3
			STATUS:2 += 1
		ELSEIF BASE:(SIDEA):1 * 100 / MAXBASE:(SIDEA):1 < S2_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%頭腦發昏。
			CFLAG:(SIDEA):534 = 3
			STATUS:5 += 1
		ELSEIF BASE:(SIDEA):0 * 100 / MAXBASE:(SIDEA):0 < S3_HP && BASE:(SIDEA):1 * 100 / MAXBASE:(SIDEA):1 < S3_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%瀕死。
			CFLAG:(SIDEA):534 = 4
			STATUS:3 += 1
		ELSEIF BASE:(SIDEA):0 * 100 / MAXBASE:(SIDEA):0 < S3_HP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%瀕死。
			CFLAG:(SIDEA):534 = 4
			STATUS:3 += 1
		ELSEIF BASE:(SIDEA):1 * 100 / MAXBASE:(SIDEA):1 < S3_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%氣絕。
			CFLAG:(SIDEA):534 = 4
			STATUS:6 += 1
		ELSE
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEA)%元氣滿滿。
			CFLAG:(SIDEA):534 = 1
			STATUS:0 += 1
		ENDIF
	ENDIF
	IF  SIDEB > 0
		IF BASE:(SIDEB):0 * 100 / MAXBASE:(SIDEB):0 < S1_HP || BASE:(SIDEB):1 * 100 / MAXBASE:(SIDEB):1 < S1_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%輕傷。
			CFLAG:(SIDEB):534 = 2
			STATUS:1 += 1
		ELSEIF BASE:(SIDEB):1 * 100 / MAXBASE:(SIDEB):1 < S1_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%身體抱恙。
			CFLAG:(SIDEB):534 = 2
			STATUS:4 += 1
		ELSEIF BASE:(SIDEB):0 * 100 / MAXBASE:(SIDEB):0 < S2_HP || BASE:(SIDEB):1 * 100 / MAXBASE:(SIDEB):1 < S2_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%重傷。
			CFLAG:(SIDEB):534 = 3
			STATUS:2 += 1
		ELSEIF BASE:(SIDEB):1 * 100 / MAXBASE:(SIDEB):1 < S2_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%頭腦發昏。
			CFLAG:(SIDEB):534 = 3
			STATUS:5 += 1
		ELSEIF BASE:(SIDEB):0 * 100 / MAXBASE:(SIDEB):0 < S3_HP && BASE:(SIDEB):1 * 100 / MAXBASE:(SIDEB):1 < S3_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%瀕死。
			CFLAG:(SIDEB):534 = 4
			STATUS:3 += 1
		ELSEIF BASE:(SIDEB):0 * 100 / MAXBASE:(SIDEB):0 < S3_HP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%瀕死。
			CFLAG:(SIDEB):534 = 4
			STATUS:3 += 1
		ELSEIF BASE:(SIDEB):1 * 100 / MAXBASE:(SIDEB):1 < S3_MP
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%氣絕。
			CFLAG:(SIDEB):534 = 4
			STATUS:6 += 1
		ELSE
			SIF !MODE
				PRINTFORML %SAVESTR:(SIDEB)%元氣滿滿。
			CFLAG:(SIDEB):534 = 1
			STATUS:0 += 1
		ENDIF
	ENDIF
ENDIF
;隊伍目前狀態評級
;基礎分0|元氣滿滿+0|輕傷+1|身體抱恙+1|重傷+3|頭腦發昏+2|瀕死+4|氣絕+4
STATUS:7 = 0 
STATUS:7 += STATUS:1
STATUS:7 += STATUS:2
STATUS:7 += (STATUS:3 * 4)
STATUS:7 += (STATUS:4 * 3)
STATUS:7 += (STATUS:5 * 2)
STATUS:7 += (STATUS:6 * 4)
RETURN STATUS,STATUS:1,STATUS:2,STATUS:3,STATUS:4,STATUS:5,STATUS:6,STATUS:7

;計算式の変更
;基礎収入に勇者のレベルを影響させる
;最終結果に現在の階層を影響させる
;------------------------------------
@GET_JUNK_ITEM,ARG
;------------------------------------
;手に入れる換金アイテム
;ダンジョンレベルが高いほど高収入のチャンス
;LOCAL = RAND:(CFLAG:MASTER:9 + 1) + 100
LOCAL = 100 + CFLAG:ARG:9 * RAND:(SQRT(CFLAG:MASTER:9 + CFLAG:ARG:9 + 1))

;好奇心ボーナス
SIF TALENT:ARG:好奇心
	LOCAL += 10
;金のためボーナス
SIF TALENT:ARG:成為勇者的契機 == 2
	LOCAL += 20
;ホビットボーナス
SIF TALENT:ARG:種族 == 10
	LOCAL += 30
;ドワーフボーナス
SIF TALENT:ARG:種族 == 11
	LOCAL += 30
;盜賊は収入が多い（1.5倍）
SIF TALENT:ARG:盜賊
	LOCAL += LOCAL / 2

LOCAL *= CFLAG:ARG:501

SIF LOCAL < 1
	LOCAL = 1
SIF FLAG:5 & 32
	PRINTFORML %SAVESTR:ARG%找到了價值{LOCAL}的財物。

CFLAG:ARG:581 += LOCAL

RETURN 0

;------------------------------------
@GET_DOWN_ENEMY,ARG
;------------------------------------
;勇者陥落時の初期化や資金入手
IF CFLAG:(ARG:0):151 <= -150 && CFLAG:(ARG:0):1 == 2
	PRINTFORML %SAVESTR:(ARG:0)%背叛了使命，向魔王軍投誠了……
ELSE
	PRINTFORML %SAVESTR:(ARG:0)%被抓住了…
ENDIF
MONEY += CFLAG:(ARG:0):580 / 100
EX_FLAG:4444 += CFLAG:(ARG:0):580 / 100
PRINTFORML {CFLAG:(ARG:0):580 / 100}Gを入手！
CFLAG:(ARG:0):580 = 0
CFLAG:(ARG:0):506 = 1
CFLAG:(ARG:0):507 = 0

RETURN 0