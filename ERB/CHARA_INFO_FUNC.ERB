

@IS_ABLE_TO_ABILITY_UP(ARG)
#FUNCTION
{
RETURNF  (CFLAG:ARG:1 == 0
		|| (CFLAG:0:9 >= 20 && CFLAG:ARG:1 == 2))
	&& BASE:ARG:0 >= 1		
}
 
@IS_ABLE_TO_CLOTH(ARG)
#FUNCTION
RETURNF CFLAG:ARG:1 == 0 && BASE:ARG:0 >= 1


@CHARA_INFO_RESTORE_STATE(ARG)
SIF CFLAG:ARG:1 == 3
	CALL PARTY_DEL,ARG

CFLAG:ARG:1 = 0

@CHARA_INFO_RECOVER_HP(ARG)
LOCAL = (MAXBASE:ARG:0 - BASE:ARG:0)*10/3 + (MAXBASE:ARG:1 - BASE:ARG:1)*5/3
IF MONEY < LOCAL
	PRINTFORMW 需要金錢{LOCAL}G，金錢不夠
	RETURN
ENDIF
PRINTFORML 要使用金錢恢復%SAVESTR:ARG%的體力和氣力嗎？
PRINTFORML 恢復{MAXBASE:ARG:0 - BASE:ARG:0}點體力和{MAXBASE:ARG:1 - BASE:ARG:1}點氣力，需花費金錢{LOCAL}G
PRINTFORML [0] 立即恢復
PRINTFORML [1] 還是算了

$INPUT_LOOP1
INPUT
IF RESULT < 0
	GOTO INPUT_LOOP1
ELSEIF RESULT > 1
	GOTO INPUT_LOOP1
ELSEIF RESULT == 1
    RETURN
ELSEIF RESULT == 0
	PRINTFORMW 花費{LOCAL}G，恢復了%SAVESTR:ARG%的體力與氣力
	MONEY -= LOCAL
	EX_FLAG:4444 -= LOCAL
	BASE:ARG:0 = MAXBASE:ARG:0
	BASE:ARG:1 = MAXBASE:ARG:1
ENDIF


@CHARA_INFO_UP_LEVEL(ARG)
LOCAL = 0, CFLAG:ARG:9
IF ARG == MASTER
	LOCAL += LOCAL:1 * 100 + 10
ELSEIF TALENT:ARG:220 == 1
	LOCAL += LOCAL:1 * 20 + 10
ELSE
	LOCAL += LOCAL:1 * 10 + 10
ENDIF
LOCAL -= EXP:ARG:80
SIF LOCAL < 0 
    LOCAL = 0

IF MONEY < LOCAL *100
    PRINTFORMW 需要金錢{LOCAL*100}G，金錢不夠
	RETURN
ENDIF

PRINTFORML 要使用金錢提升%SAVESTR:ARG%的等級嗎？
PRINTFORML 到下一級經驗還要{LOCAL}點，需花費金錢{LOCAL*100}G
PRINTFORML [0] 提升等級
PRINTFORML [1] 還是算了

$INPUT_LOOP2
INPUT
IF RESULT < 0
	GOTO INPUT_LOOP2
ELSEIF RESULT > 1
	GOTO INPUT_LOOP2
ELSEIF RESULT == 1
    RETURN
ELSEIF RESULT == 0
	LOCALS = \@ ARG == MASTER ? %NAME:MASTER% # %SAVESTR:ARG% \@
	PRINTFORMW %NAME:MASTER%花費了{LOCAL*100}G，購買了經驗{LOCAL}點
	MONEY -= LOCAL *100
	EX_FLAG:4444 -= LOCAL *100
	EXP:ARG:80 += LOCAL
	CALL LVUP, ARG
ENDIF


@CHARA_INFO_CALLBACK(ARG)
IF CFLAG:ARG:9 >= CFLAG:MASTER:9
	PRINTFORMW LV{CFLAG:MASTER:9}的%SAVESTR:MASTER%無法對LV{CFLAG:ARG:9}的%SAVESTR:ARG%發動傳送魔法
	RETURN
ENDIF

LOCAL = CFLAG:ARG:9 * 100 / CFLAG:MASTER:9
LOCAL = LOCAL * LOCAL * MAXBASE:MASTER:1 / 10000

IF LOCAL > BASE:MASTER:1
	PRINTFORMW %NAME:MASTER%的氣力不足以對%SAVESTR:ARG%發動傳送魔法（需要{LOCAL}點，目前{BASE:MASTER:1}點）
	RETURN
ENDIF

PRINTFORML 要召回出擊中的%SAVESTR:ARG%嗎？
PRINTFORML 發動傳送魔法需要消耗%NAME:MASTER%的氣力{LOCAL}點
PRINTFORML [0] 立即召回
PRINTFORML [1] 還是算了

$INPUT_LOOP3
INPUT
IF RESULT < 0
	GOTO INPUT_LOOP3
ELSEIF RESULT > 1
	GOTO INPUT_LOOP3
ELSEIF RESULT == 1
    RETURN
ELSEIF RESULT == 0
    BASE:MASTER:1 -= LOCAL
    PRINTFORMW %NAME:MASTER%使用魔法把%SAVESTR:ARG%傳送了回來！
    CALL PARTY_DEL,ARG
    CFLAG:ARG:1 = 0
ENDIF

