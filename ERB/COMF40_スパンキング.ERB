;eraIM@Sから導入しました(eramaou)

;-------------------------------------------------
;打屁股
;SM系コマンド
;-------------------------------------------------
@COM40

;-------------------------------------------------
;跳轉到進階指令
;-------------------------------------------------
LOCAL = 40
CALL GET_ADV_COM,LOCAL
SIF RESULT != LOCAL
	JUMPFORM COM{RESULT}


PRINTL 打屁股
;SAVESTR:22 = 打屁股
CALL TRAIN_MESSAGE_B

;実際には苦痛があるためもっと體力気力をもっていかれる
LOSEBASE:0 += 80
LOSEBASE:1 += 40

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
SOURCE:12 = 200
SOURCE:14 = 500

;PALAM:苦痛をみる
IF PALAM:9 < PALAMLV:1
	SOURCE:6 = 300
ELSEIF PALAM:9 < PALAMLV:2
	SOURCE:6 = 500
ELSEIF PALAM:9 < PALAMLV:3
	SOURCE:6 = 800
ELSEIF PALAM:9 < PALAMLV:4
	SOURCE:6 = 1200
ELSEIF PALAM:9 >= PALAMLV:4
	SOURCE:6 = 1800
ENDIF

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +2
	EXP:40 += 2
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +2
	EXP:41 += 2
ENDIF

SIF ASSIPLAY == 0 && ABL:21 >= 1
	TFLAG:30 += 1

;愛情經驗
E = 1
IF CFLAG:2 >= 1000 && (ABL:21 >= 3 || TALENT:88) && ASSIPLAY == 0
	PRINTFORML %EXPNAME:23%+{E}
	EXP:23 += E
ENDIF
E = 0

RETURN 1
;
