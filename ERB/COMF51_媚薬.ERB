;eraIM@Sから導入しました(eramaou)

;-------------------------------------------------
;しあわせ草（媚藥）
;-------------------------------------------------
;他のeraシリーズでは媚藥にあたるアイテム
;CFLAG:31=體內媚藥殘留度
;CFLAG:32=しあわせ中毒の禁斷癥狀判定
;-------------------------------------------------
@COM51

PRINTL 媚藥
;SAVESTR:22 = 媚藥
CALL TRAIN_MESSAGE_B

;-------------------------------------------------
;ソースの計算
;-------------------------------------------------
LOSEBASE:0 += 300
LOSEBASE:1 += 300
;主人か助手に[調合知識]があれば體力・気力消費を抑える
IF TALENT:MASTER:55
	LOSEBASE:0 -= 150
	LOSEBASE:1 -= 150
ELSEIF ASSI >= 0
	IF TALENT:ASSI:55
		LOSEBASE:0 -= 150
		LOSEBASE:1 -= 150
	ENDIF
ENDIF
;藥物經驗が多いほど消費體力は減っていく
IF EXP:57 < EXPLV:1
	LOSEBASE:0 += 300
ELSEIF EXP:57 < EXPLV:2
	LOSEBASE:0 += 50
ELSEIF EXP:57 < EXPLV:3
	LOSEBASE:0 += 0
ELSEIF EXP:57 < EXPLV:5
	LOSEBASE:0 -= 50
ELSE
	LOSEBASE:0 -= 100
ENDIF

SOURCE:14 = 2000

;しあわせ草中毒なら體力消費を抑えかつ効果も上げる
IF TALENT:46
	LOSEBASE:0 -= 100
	SOURCE:7 = 500
	SOURCE:14 = 1000
	;媚藥効果で欲情のアップが2倍になるので実際には+20000
	SOURCE:11 = 10000
ELSE
	SOURCE:14 = 2000
	;媚藥効果で欲情のアップが2倍になるので実際には+10000
	SOURCE:11 = 5000
ENDIF

;體力消費が0以下になるという事はない
SIF LOSEBASE:0 < 0
	LOSEBASE:0 = 0

;-------------------------------------------------
;経験上昇
;-------------------------------------------------
;レズ・ホモ経験
IF TALENT:122 == 0 && TALENT:PLAYER:122 == 0
	PRINTS EXPNAME:40
	PRINTL +1
	EXP:40 += 1
ELSEIF TALENT:122 == 1 && TALENT:PLAYER:122 == 1
	PRINTS EXPNAME:41
	PRINTL +1
	EXP:41 += 1
ENDIF

;藥物經驗
PRINTFORML %EXPNAME:57%+1
EXP:57 += 1
;-------------------------------------------------
;その他の処理
;-------------------------------------------------
;しあわせ草使用フラグ
TEQUIP:21 = 1

;アイテム消費
SIF TEQUIP:90 == 0
	ITEM:26 -= 1

;しあわせ草殘留濃度
CFLAG:31 += 1

;しあわせ中毒なら禁斷癥狀迴避フラグを立てる
SIF TALENT:46
	CFLAG:32 = 1

RETURN 1

