;eraIM@Sから導入しました(eramaou)

;-------------------------------------------------
;売卻と助手化のチェック
;-------------------------------------------------
@CHECK_SELLASSIABLE

SIF TARGET < 0 || TARGET >= CHARANUM
	RETURN 0

;順從+慾望が6LV以上であるか
SIF ABL:10+ABL:11 < 6
	RETURN 0
;C感覚・V感覚・A感覚・B感覚のいずれかが最低3LVであり
SIF ABL:0 < 3 && ABL:1 < 3 && ABL:2 < 3 && ABL:3 < 3
	RETURN 0
;【反抗心】か【剛強】を持っているなら順從が4LV以上になっており
SIF ABL:10 < 4 && (TALENT:11 || TALENT:12)
	RETURN 0
;【克制】【壓抑】【抵抗】を持っているなら慾望が4LV以上になっており
SIF ABL:11 < 4 && (TALENT:20 || TALENT:32 || TALENT:34)
	RETURN 0
;「〈技巧〉3LV以上で〈侍奉精神〉3LV以上」か「〈露出癖〉3LV以上で〈自慰中毒〉2LV以上」か「C・V・A・B感覚の合計が13LV以上」か「順從か慾望がLV5」に達しているか
IF (ABL:12 >= 3 && ABL:16 >= 3) || (ABL:17 >= 3 && ABL:31 >= 2) || ABL:21 >= 3 || (ABL:0+ABL:1+ABL:2+ABL:3 >= 13) || ABL:10 >= 5 || ABL:11 >= 5
	IF CFLAG:0 <= 0
		PRINTFORMW %SAVESTR:TARGET%可以賣掉了
		CFLAG:0 = 1
	ENDIF
ELSE
	RETURN 0
ENDIF

;「〈順從〉〈慾望〉〈技巧〉〈陰蒂感覺〉〈百合氣質〉がすべて3LV以上」か「〈順從〉が5LV以上かつ〈慾望〉が4LV以上」か
IF (ABL:10 >= 3 && ABL:11 >= 3 && ABL:12 >= 3 && ABL:0 >= 3 && ABL:22 >= 3) || (ABL:10 >= 5 && ABL:11 >= 4)
	IF CFLAG:0 <= 1
		PRINTFORMW %SAVESTR:TARGET%可以做調教助手了
		CFLAG:0 = 2
	ENDIF
ENDIF
;
;-------------------------------------------------
;奴隷売卻の処理
;-------------------------------------------------
@CHARA_SALE
#DIM PRICE_N
#DIMS PRICE_S
DRAWLINE
PRINTV DAY+1
PRINT 日
IF TIME == 0
	PRINTL  午前
ELSE
	PRINTL  午後
ENDIF

PRINTFORML 所持金：${MONEY}點
DRAWLINE

$INPUT_LOOP

PRINTL 要賣掉誰呢？
CUSTOMDRAWLINE ‥
REPEAT CHARANUM
	IF COUNT == 0
		CONTINUE
	;売れないキャラは排除
	ELSEIF CFLAG:COUNT:0 < 1
		CONTINUE
	;臨死中のキャラは排除
	ELSEIF  BASE:COUNT:0 < 1
		CONTINUE
	;魔王の影は排除
	ELSEIF TALENT:COUNT:292
		CONTINUE
	ELSEIF CFLAG:COUNT:1 == 0
		PRINTFORM [{COUNT}] %SAVESTR:COUNT, 16, LEFT% 
		TARGET = COUNT
		S = 0
		CALL ESTIMATE_CHARA
		PRICE_N = S
		PRICE_S = 
		WHILE PRICE_N > 0
			IF PRICE_N < 1000
				PRICE_S = {PRICE_N % 1000}%PRICE_S%
			ELSE
				PRICE_S = ,{PRICE_N % 1000 / 100}{PRICE_N % 100 / 10}{PRICE_N % 10}%PRICE_S%
			ENDIF
			PRICE_N /= 1000
		WEND
		IF S > 0
			PRINTFORM [評價額:%PRICE_S, 13%點]
		ELSE
			PRINTFORM [不能賣掉]
		ENDIF
		IF ISASSI:COUNT == 1
			PRINT (原助手)
		ELSEIF CFLAG:COUNT:1 == 2
			PRINT (可做助手)
		ENDIF
		SIF CFLAG:COUNT:700
			PRINT [☆]
		PRINTL  
	ENDIF
REND
CUSTOMDRAWLINE ‥
PRINTFORML [999] - 返回

INPUT

IF RESULT == 999
	TARGET = FLAG:1
	RETURN 999
ELSEIF RESULT <= 0 || RESULT >= CHARANUM
	GOTO INPUT_LOOP
ELSEIF CFLAG:RESULT:0 < 1
	GOTO INPUT_LOOP
;臨死中のキャラは排除
ELSEIF BASE:RESULT:0 < 1
	GOTO INPUT_LOOP
;営業中のキャラは排除
ELSEIF  CFLAG:RESULT:12 && TIME == 1
	PRINTL 奴隸市場，只開上午。下午是結算與交接的時間。
	WAIT
	GOTO INPUT_LOOP
;排除收藏中的角色
ELSEIF CFLAG:RESULT:700
	PRINTFORMW %SAVESTR:RESULT%在你的收藏列表里，不能賣掉。
	GOTO INPUT_LOOP
;魔王の影は排除
ELSEIF TALENT:RESULT:292
	PRINTFORMW %SAVESTR:RESULT%只是影子，一旦與你分離便會煙消雲散，因此無法賣掉。
	GOTO INPUT_LOOP
ENDIF

;現在の調教対象の情報を取得
SIF TARGET > 0
	T = NO:TARGET

TARGET = RESULT

;売卻処理
S = 0
CALL SALE_CHARA

;長いお別れ
SIF S > 0
	CALL LONG_GOOD_BYE

;売卻した場合はキャラを削除
IF S > 0
	T = -1
	CALL KILL_TARGET
ENDIF

;対象を戻す
TARGET = -1
REPEAT CHARANUM
	SIF NO:COUNT == T
		TARGET = COUNT
REND



RESTART
;
;-------------------------------------------------
;現在の調教対象の削除
;-------------------------------------------------
@KILL_TARGET
X = NO:TARGET + 199
FLAG:X = 1

CALL PARTY_CHAR_DEL, TARGET

;キャラ削除
DELCHARA TARGET

;前回の助手・調教対象だった場合はフラグを空に
SIF FLAG:1 == TARGET
	FLAG:1 = -1
SIF FLAG:2 == TARGET
	FLAG:2 = -1

;前回の助手・調教対象より前だった場合はフラグを減算
SIF FLAG:1 > TARGET
	FLAG:1 -= 1
SIF FLAG:2 > TARGET
	FLAG:2 -= 1

;調教対象を空に
TARGET = FLAG:1

;助手を空に
ASSI = FLAG:2

CALL NAME_RESET
;REPEAT CHARANUM
	;IF COUNT >= 1
		;A = COUNT
		;CALL NAMING
	;ENDIF 
;REND
;
;-------------------------------------------------
;長いお別れ
;-------------------------------------------------
@LONG_GOOD_BYE
A = NO:TARGET

REPEAT CHARANUM
	SIF COUNT == 0
		CONTINUE
	;キャラのストレス値
	U = 0
	SIF A == (CFLAG:COUNT:21 % 100) && (CFLAG:COUNT:21 / 100) < 2
		U += 40
	SIF A == (CFLAG:COUNT:21 % 100)
		U += 80
	SIF A == (CFLAG:COUNT:22 % 100) && (CFLAG:COUNT:22 / 100) < 2
		U += 40
	SIF A == (CFLAG:COUNT:22 % 100)
		U += 80
	SIF A == (CFLAG:COUNT:23 % 100) && (CFLAG:COUNT:23 / 100) < 2
		U += 40
	SIF A == (CFLAG:COUNT:23 % 100)
		U += 80
	SIF A == (CFLAG:COUNT:24 % 100) && (CFLAG:COUNT:24 / 100) < 2
		U += 40
	SIF A == (CFLAG:COUNT:24 % 100)
		U += 80
	SIF A == (CFLAG:COUNT:25 % 100) && (CFLAG:COUNT:25 / 100) < 2
		U += 40
	SIF A == (CFLAG:COUNT:25 % 100)
		U += 80
	SIF RELATION:COUNT:A > 100
		U += RELATION:COUNT:A - 100
	IF U > 50
		DRAWLINE
		PRINTL 
		PRINTFORML 那天，%SAVESTR:COUNT%被賣掉的%SAVESTR:TARGET%坐在馬車上，
		SIF TALENT:COUNT:45 == 0
			PRINT 淚眼朦朧了。
		PRINTFORML 視在脫離視野之前，
		PRINTFORML 甚至在離開視野之後，也一直在目送著。
		WAIT
		;愛慕
		SIF TALENT:COUNT:85
			U -= 120
		;淫亂
		SIF TALENT:COUNT:76
			U -= 60
		;剛強
		SIF TALENT:COUNT:12
			U -= 20
		;感情淡薄
		SIF TALENT:COUNT:22
			U -= 20
		;軟弱
		SIF TALENT:COUNT:134
			U += 20
		;順從
		U -= ABL:COUNT:10 * 10
		;反抗刻印
		U += MARK:COUNT:3 * 30

		;ストレス値の累積が100を超えた場合
		IF U >= 100 && TALENT:COUNT:9 == 0
		DRAWLINE
			PRINTFORML %CALLNAME:COUNT%在目瞪口呆的表情中，
			PRINTFORML %CALLNAME:COUNT%心裡什麼東西壞掉了……
			PRINTFORML %CALLNAME:COUNT%的精神【%TALENTNAME:9%】了。
			WAIT
			IF TALENT:COUNT:85
				PRINTFORML %CALLNAME:COUNT%的【%TALENTNAME:85%】失去了。
				TALENT:COUNT:85 = 0
			ENDIF
			IF TALENT:COUNT:76
				PRINTFORML %CALLNAME:COUNT%的【%TALENTNAME:76%】失去了。
				TALENT:COUNT:76 = 0
			ENDIF
			TALENT:COUNT:9 = 1
			WAIT
		ENDIF
	ENDIF
REND
;-------------------------------------------------
;奴隷売卻
;-------------------------------------------------
@SALE_CHARA

;売卻額
S = 0
CALL ESTIMATE_CHARA

;評価表示
SIF A:10 > 0
	PRINTFORML %ABLNAME:10, 8, LEFT% LV{ABL:10, 2} ＋{A:10, 5}
SIF A:11 > 0
	PRINTFORML %ABLNAME:11, 8, LEFT% LV{ABL:11, 2} ＋{A:11, 5}
SIF A:12 > 0
	PRINTFORML %ABLNAME:12, 8, LEFT% LV{ABL:12, 2} ＋{A:12, 5}
SIF A:13 > 0
	PRINTFORML %ABLNAME:13, 8, LEFT% LV{ABL:13, 2} ＋{A:13, 5}
SIF A:14 > 0
	PRINTFORML %ABLNAME:14, 8, LEFT% LV{ABL:14, 2} ＋{A:14, 5}
SIF A:15 > 0
	PRINTFORML %ABLNAME:15, 8, LEFT% LV{ABL:15, 2} ＋{A:15, 5}
SIF A:30 > 0
	PRINTFORML %ABLNAME:30, 8, LEFT% LV{ABL:30, 2} ＋{A:30, 5}
SIF A:31 > 0
	PRINTFORML %ABLNAME:31, 8, LEFT% LV{ABL:31, 2} ＋{A:31, 5}
SIF A:32 > 0
	PRINTFORML %ABLNAME:32, 8, LEFT% LV{ABL:32, 2} ＋{A:32, 5}
SIF A:33 > 0
	PRINTFORML %ABLNAME:33, 8, LEFT% LV{ABL:33, 2} ＋{A:33, 5}
SIF A:39 > 0
	PRINTFORML %ABLNAME:39, 8, LEFT% LV{ABL:39, 2} ＋{A:39, 5}
SIF A:37 > 0
	PRINTFORML %ABLNAME:37, 8, LEFT% LV{ABL:37, 2} －{A:37, 5}
SIF B:0 > 0
	PRINTFORML %"陰核感覺封鎖",12,LEFT% －{B:0, 5}
SIF B:1 > 0
	PRINTFORML %"私處感覺封鎖",12,LEFT% －{B:1, 5}
SIF B:2 > 0
	PRINTFORML %"肛門感覺封鎖",12,LEFT% －{B:2, 5}
SIF B:3 > 0
	PRINTFORML %"乳房感覺封鎖",12,LEFT% －{B:3, 5}

SIF A:0 != 100
	PRINTFORML %ABLNAME:0, 8, LEFT% LV{ABL:0, 2} × {A:0/100}.{A:0%100/10}{A:0%10}
SIF A:1 != 100
	PRINTFORML %ABLNAME:1, 8, LEFT% LV{ABL:1, 2} × {A:1/100}.{A:1%100/10}{A:1%10}
SIF A:2 != 100
	PRINTFORML %ABLNAME:2, 8, LEFT% LV{ABL:2, 2} × {A:2/100}.{A:2%100/10}{A:2%10}
SIF A:3 != 100
	PRINTFORML %ABLNAME:3, 8, LEFT% LV{ABL:3, 2} × {A:3/100}.{A:3%100/10}{A:3%10}
SIF A:16 != 100
	PRINTFORML %ABLNAME:16, 8, LEFT% LV{ABL:16, 2} × {A:16/100}.{A:16%100/10}{A:16%10}
SIF A:17 != 100
	PRINTFORML %ABLNAME:17, 8, LEFT% LV{ABL:17, 2} × {A:17/100}.{A:17%100/10}{A:17%10}
SIF A:20 != 100
	PRINTFORML %ABLNAME:20, 8, LEFT% LV{ABL:20, 2} × {A:20/100}.{A:20%100/10}{A:20%10}
SIF A:21 != 100
	PRINTFORML %ABLNAME:21, 8, LEFT% LV{ABL:21, 2} × {A:21/100}.{A:21%100/10}{A:21%10}
SIF A:22 != 100
	PRINTFORML %ABLNAME:22, 8, LEFT% LV{ABL:22, 2} × {A:22/100}.{A:22%100/10}{A:22%10}
SIF A:23 != 100
	PRINTFORML %ABLNAME:23, 8, LEFT% LV{ABL:23, 2} × {A:23/100}.{A:23%100/10}{A:23%10}

SIF E:74 != 100
	PRINTFORML %EXPNAME:74, 8, LEFT%{EXP:74, 4} × {E:74/100}.{E:74%100/10}{E:74%10}
SIF E:60 != 100
	PRINTFORML %EXPNAME:60, 8, LEFT%{EXP:60, 4} × {E:60/100}.{E:60%100/10}{E:60%10}

;素質による倍率表示
REPEAT 300
	SIF T:COUNT != 100
		PRINTFORML %TALENTNAME:COUNT, 12, LEFT% × {T:COUNT/100}.{T:COUNT%100/10}{T:COUNT%10}
REND

SIF T:310 != 100
	PRINTFORML %"稀有人物",12,LEFT% × {T:310/100}.%TOSTR(T:310%100,"00")%

;SIF T:311 != 100
	;PRINTFORML %"原勇者",12,LEFT% × {T:311/100}.%TOSTR(T:311%100,"00")%

SIF O:0 != 100
	PRINTFORML %"原助手",12,LEFT% × {O:0/100}.%TOSTR(O:0%100,"00")%

PRINT 種族：
PRINTFORML %GET_LOOK_INFO(TARGET,"種族"),12,LEFT% × {T:314/100}.%TOSTR(T:314%100,"00")%

IF O:1 != 100
	PRINTFORM 價錢談判 × {O:1/100}.
	SIF O:1 > 100 && O:1 < 110
		PRINT 0
	PRINTV TOSTR(O:1%100,"00")
ENDIF

;売卻額表示
PRINTFORML %SAVESTR:TARGET%能賣出{S}點的樣子。
PRINTFORML 把%SAVESTR:TARGET%賣掉嗎？
PRINTL  [0] - 好的
PRINTL  [1] - 不要

$INPUT_LOOP
INPUT

IF RESULT == 0
	;売卻時口上
	TFLAG:13 = 6
	CALL SELF_KOJO

	PRINTFORMW %SAVESTR:TARGET%以{S}點賣掉了。
	MONEY += S
	EX_FLAG:4444 += S
	IF TALENT:TARGET:220 != 1 && EX_TALENT:TARGET:1 != 1 
	EX_FLAG:99 += 5
	PRINTFORML 威望值增加
	ELSE
	EX_FLAG:99 -= 10
	PRINTFORML 威望值減少
	ENDIF
	RETURN 1
ELSEIF RESULT == 1
	S = -1
	RETURN 0
ELSE
	GOTO INPUT_LOOP
ENDIF
;
;
;